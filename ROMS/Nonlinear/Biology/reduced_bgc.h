      MODULE biology_mod
      implicit none
!
      PRIVATE
      PUBLIC  :: biology
!
      CONTAINS

      SUBROUTINE biology (ng,tile)
!
!svn $Id$
!***********************************************************************
!  Copyright (c) 2002-2021 The ROMS/TOMS Group                         !
!    Licensed under a MIT/X style license           Hernan G. Arango   !
!                                                       Katja Fennel   !
!    See License_ROMS.txt                             Arnaud Laurent   !
!****************************************** Alexander F. Shchepetkin ***
!                                                                      !
!  This routine computes the  biological sources and sinks for the     !
!  Simple BGC model. Then, it  adds  those  terms  to  the  global     !
!  biological fields.  This routine builds on the biological model     !
!  of Fennel et al. (2006, 2008).                                      !
!                                                                      !
!  The simulation of  inorganic carbon and dissolved oxygen can be     !
!  activated independently with the options "CARBON" and "OXYGEN",     !
!  respectively. One of the two options is mandatory.                  !
!                                                                      !
!  If the "pCO2_RZ" options is activated, in addition to "CARBON",     !
!  the carbonate system  routines by Zeebe and Wolf-Gladrow (2001)     !
!  are used,  while the  OCMIP  standard routines are the default.     !
!  Alkalinity is treated diagnostically as a function of salinity.     !
!                                                                      !
!  If "OCMIP_OXYGEN_SC" is used the  Schmidt  number  of oxygen in     !
!  seawater will be  computed  using the  formulation  proposed by     !
!  Keeling et al. (1998, Global Biogeochem. Cycles,  12, 141-163).     !
!  Otherwise,  the Wanninkhof (1992)  formula will be used. See        !
!  Fennel et al. (2013) for more details.                              !
!                                                                      !
!  If the "RW14_OXYGEN_SC" and/or  "RW14_CO2_SC" options are used,     !
!  the model will use Wanninkhof (2014) air-sea flux parameteri-       !
!  zation.   With the  "TALK_NONCONSERV"  option,   alkalinity  is     !
!  affected by biological fluxes   as described in  Laurent et al.     !
!  (2017).                                                             !
!                                                                      !
!  With the  "PCO2AIR_DATA"  option,   atmospheric pCO2  uses  the     !
!  climatology of  Laurent et al. (2017).   The  "PCO2AIR_MAUNALOA"    !
!  and "PCO2AIR_SABLEISLAND"  options  provide an alternative time-    !
!  dependent  atmospheric  pCO2  evolution  that  matches the          !
!  observations from the Mauna Loa observatory (1958-2020) or from     !
!  Sable Island (Rutherford et al., 2021),  respectively.  If none     !
!  of the 2 options are defined, atmospheric pCO2 is constant.         !
!                                                                      !
!  Temperature dependent water column respiration can be activated     !
!  with the option "TEMP_RATES" (see Laurent et al., 2021).            !
!                                                                      !
!***********************************************************************

!  References:                                                         !
!                                                                      !
!    Fennel, K., Wilkin, J., Levin, J., Moisan, J., O^Reilly, J.,      !
!      Haidvogel, D., 2006: Nitrogen cycling in the Mid Atlantic       !
!      Bight and implications for the North Atlantic nitrogen          !
!      budget: Results from a three-dimensional model.  Global         !
!      Biogeochemical Cycles 20, GB3007, doi:10.1029/2005GB002456.     !
!                                                                      !
!    Fennel, K., Wilkin, J., Previdi, M., Najjar, R. 2008:             !
!      Denitrification effects on air-sea CO2 flux in the coastal      !
!      ocean: Simulations for the Northwest North Atlantic.            !
!      Geophys. Res. Letters 35, L24608, doi:10.1029/2008GL036147.     !
!                                                                      !
!    Fennel, K., Hu, J., Laurent, A., Marta-Almeida, M., Hetland, R.   !
!      2013: Sensitivity of Hypoxia Predictions for the Northern Gulf  !
!      of Mexico to Sediment Oxygen Consumption and Model Nesting. J.  !
!      Geophys. Res. Ocean 118 (2), 990-1002, doi:10.1002/jgrc.20077.  !
!                                                                      !
!    Laurent, A., Fennel, K., Hu, J., Hetland, R. 2012: Simulating     !
!      the Effects of Phosphorus Limitation in the Mississippi and     !
!      Atchafalaya River Plumes. Biogeosciences, 9 (11), 4707-4723,    !
!      doi:10.5194/bg-9-4707-2012.                                     !
!                                                                      !
!    Laurent, A., Fennel, K., Kuhn, A. 2021: An observation-based      !
!      evaluation and ranking of historical Earth system model         !
!      simulations in the northwest North Atlantic Ocean.              !
!      Biogeosciences, 18 (5), 1803–1822, doi:10.5194/bg-18-1803-2021. ! 
!                                                                      !
!    Rutherford, K. and Fennel, K. and Atamanchuk, D. and Wallace, D.  !
!      and Thomas, H., 2021: A modelling study of temporal and spatial !
!      pCO2 variability on the biologically active and temperature-    !
!      dominated Scotian Shelf. Biogeosciences, 18 (23), 6271-6286,    !
!      doi: 10.5194/bg-18-6271-2021.                                   !
!                                                                      !
!    Wanninkhof, R. 2014: Relationship between Wind Speed and Gas      !
!      Exchange over the Ocean Revisited. Limnol. Oceanogr. Methods    !
!      12 (6), 351-362, doi:10.4319/lom.2014.12.351.                   !
!                                                                      !
!***********************************************************************
!
      USE mod_param
#ifdef DIAGNOSTICS_BIO
      USE mod_diags
#endif
      USE mod_forces
      USE mod_grid
      USE mod_ncparam
      USE mod_ocean
      USE mod_stepping
!
      implicit none
!
!  Imported variable declarations.
!
      integer, intent(in) :: ng, tile
!
!  Local variable declarations.
!
      character (len=*), parameter :: MyFile =                          &
     &  __FILE__
!
#include "tile.h"
!
!  Set header file name.
!
#ifdef DISTRIBUTE
      IF (Lbiofile(iNLM)) THEN
#else
      IF (Lbiofile(iNLM).and.(tile.eq.0)) THEN
#endif
        Lbiofile(iNLM)=.FALSE.
        BIONAME(iNLM)=MyFile
      END IF
!
#ifdef PROFILE
      CALL wclock_on (ng, iNLM, 15, __LINE__, MyFile)
#endif
      CALL biology_tile (ng, tile,                                      &
     &                   LBi, UBi, LBj, UBj, N(ng), NT(ng),             &
     &                   IminS, ImaxS, JminS, JmaxS,                    &
     &                   nstp(ng), nnew(ng),                            &
#ifdef MASKING
     &                   GRID(ng) % rmask,                              &
# ifdef WET_DRY
     &                   GRID(ng) % rmask_wet,                          &
#  ifdef DIAGNOSTICS_BIO
     &                   GRID(ng) % rmask_full,                         &
#  endif
# endif
#endif
     &                   GRID(ng) % Hz,                                 &
     &                   GRID(ng) % z_r,                                &
     &                   GRID(ng) % z_w,                                &
     &                   GRID(ng) % lonr,                               &
     &                   GRID(ng) % latr,                               &
#if defined TALK_ADDITION
     &                   GRID(ng) % pm,                                 &
     &                   GRID(ng) % pn,                                 &
# ifdef TALK_FILE
     &                   FORCES(ng) % taflx,                            &
     &                   FORCES(ng) % tatype,                           &
     &                   FORCES(ng) % takmin,                           &
     &                   FORCES(ng) % takmax,                           &
# endif
#endif
#if defined CARBON || defined OXYGEN
# ifdef BULK_FLUXES
     &                   FORCES(ng) % Uwind,                            &
     &                   FORCES(ng) % Vwind,                            &
# else
     &                   FORCES(ng) % sustr,                            &
     &                   FORCES(ng) % svstr,                            &
# endif
#endif
#ifdef CARBON
     &                   OCEAN(ng) % pH,                                &
     &                   OCEAN(ng) % pHc,                               &
# ifdef TALK_ADDITION
     &                   OCEAN(ng) % pHa,                               &
# endif
#endif
#ifdef DIAGNOSTICS_BIO
     &                   DIAGS(ng) % DiaBio2d,                          &
#endif
     &                   OCEAN(ng) % t)

#ifdef PROFILE
      CALL wclock_off (ng, iNLM, 15, __LINE__, MyFile)
#endif
!
      RETURN
      END SUBROUTINE biology
!
!-----------------------------------------------------------------------
      SUBROUTINE biology_tile (ng, tile,                                &
     &                         LBi, UBi, LBj, UBj, UBk, UBt,            &
     &                         IminS, ImaxS, JminS, JmaxS,              &
     &                         nstp, nnew,                              &
#ifdef MASKING
     &                         rmask,                                   &
# if defined WET_DRY
     &                         rmask_wet,                               &
#  ifdef DIAGNOSTICS_BIO
     &                         rmask_full,                              &
#  endif
# endif
#endif
     &                         Hz, z_r, z_w,                            &
     &                         lonr, latr,                              &
# ifdef TALK_ADDITION
     &                         pm, pn,                                  &
#  ifdef TALK_FILE
     &                         taflx,                                   &
     &                         tatype,                                  &
     &                         takmin,                                  &
     &                         takmax,                                  &
#  endif
# endif
#if defined CARBON || defined OXYGEN
# ifdef BULK_FLUXES
     &                         Uwind, Vwind,                            &
# else
     &                         sustr, svstr,                            &
# endif
#endif
#ifdef CARBON
     &                         pH, pHc,                                 &
# ifdef TALK_ADDITION
     &                         pHa,                                     &
# endif
#endif
#ifdef DIAGNOSTICS_BIO
     &                         DiaBio2d,                                &
#endif
     &                         t)
!-----------------------------------------------------------------------
!
      USE mod_param
      USE mod_biology
      USE mod_ncparam
      USE mod_scalars
!
      USE dateclock_mod, ONLY : caldate
!
!  Imported variable declarations.
!
      integer, intent(in) :: ng, tile
      integer, intent(in) :: LBi, UBi, LBj, UBj, UBk, UBt
      integer, intent(in) :: IminS, ImaxS, JminS, JmaxS
      integer, intent(in) :: nstp, nnew

!#ifdef ASSUMED_SHAPE
!# ifdef MASKING
!      real(r8), intent(in) :: rmask(LBi:,LBj:)
!#  ifdef WET_DRY
!      real(r8), intent(in) :: rmask_wet(LBi:,LBj:)
!#   ifdef DIAGNOSTICS_BIO
!      real(r8), intent(in) :: rmask_full(LBi:,LBj:)
!#   endif
!#  endif
!# endif
!      real(r8), intent(in) :: Hz(LBi:,LBj:,:)
!      real(r8), intent(in) :: z_r(LBi:,LBj:,:)
!      real(r8), intent(in) :: z_w(LBi:,LBj:,0:)
!      real(r8), intent(in) :: lonr(LBi:,LBj:)
!      real(r8), intent(in) :: latr(LBi:,LBj:)
!# if defined CARBON || defined OXYGEN
!#  ifdef BULK_FLUXES
!      real(r8), intent(in) :: Uwind(LBi:,LBj:)
!      real(r8), intent(in) :: Vwind(LBi:,LBj:)
!#  else
!      real(r8), intent(in) :: sustr(LBi:,LBj:)
!      real(r8), intent(in) :: svstr(LBi:,LBj:)
!#  endif
!# endif
!# ifdef CARBON
!      real(r8), intent(inout) :: pH(LBi:,LBj:)
!      real(r8), intent(inout) :: pHc(LBi:,LBj:)
!#  ifdef TALK_ADDITION
!      real(r8), intent(inout) :: pHa(LBi:,LBj:)
!      real(r8), intent(in) :: pm(LBi:,LBj:)
!      real(r8), intent(in) :: pn(LBi:,LBj:)
!#   ifdef TALK_FILE
!      real(r8), intent(in) :: taflx(LBi:,LBj:)
!      real(r8), intent(in) :: tatype(LBi:,LBj:)
!      real(r8), intent(in) :: takmin(LBi:,LBj:)
!      real(r8), intent(in) :: takmax(LBi:,LBj:)
!#   endif
!#  endif
!# endif
!# ifdef DIAGNOSTICS_BIO
!      real(r8), intent(inout) :: DiaBio2d(LBi:,LBj:,:)
!# endif
!      real(r8), intent(inout) :: t(LBi:,LBj:,:,:,:)
!#else
# ifdef MASKING
      real(r8), intent(in) :: rmask(LBi:UBi,LBj:UBj)
#  ifdef WET_DRY
      real(r8), intent(in) :: rmask_wet(LBi:UBi,LBj:UBj)
#   ifdef DIAGNOSTICS_BIO
      real(r8), intent(in) :: rmask_full(LBi:UBi,LBj:UBj)
#   endif
#  endif
# endif
      real(r8), intent(in) :: Hz(LBi:UBi,LBj:UBj,UBk)
      real(r8), intent(in) :: z_r(LBi:UBi,LBj:UBj,UBk)
      real(r8), intent(in) :: z_w(LBi:UBi,LBj:UBj,0:UBk)
      real(r8), intent(in) :: lonr(LBi:UBi,LBj:UBj)  
      real(r8), intent(in) :: latr(LBi:UBi,LBj:UBj) 
# if defined CARBON || defined OXYGEN
#  ifdef BULK_FLUXES
      real(r8), intent(in) :: Uwind(LBi:UBi,LBj:UBj)
      real(r8), intent(in) :: Vwind(LBi:UBi,LBj:UBj)
#  else
      real(r8), intent(in) :: sustr(LBi:UBi,LBj:UBj)
      real(r8), intent(in) :: svstr(LBi:UBi,LBj:UBj)
#  endif
# endif
# ifdef CARBON
      real(r8), intent(inout) :: pH(LBi:UBi,LBj:UBj)
      real(r8), intent(inout) :: pHc(LBi:UBi,LBj:UBj)
#  ifdef TALK_ADDITION
      real(r8), intent(inout) :: pHa(LBi:UBi,LBj:UBj)
      real(r8), intent(in) :: pm(LBi:UBi,LBj:UBj)
      real(r8), intent(in) :: pn(LBi:UBi,LBj:UBj)
#   ifdef TALK_FILE
      real(r8), intent(in) :: taflx(LBi:UBi,LBj:UBj)
      real(r8), intent(in) :: tatype(LBi:UBi,LBj:UBj)
      real(r8), intent(in) :: takmin(LBi:UBi,LBj:UBj)
      real(r8), intent(in) :: takmax(LBi:UBi,LBj:UBj)
#   endif
#  endif
# endif
# ifdef DIAGNOSTICS_BIO
      real(r8), intent(inout) :: DiaBio2d(LBi:UBi,LBj:UBj,NDbio2d)
# endif
      real(r8), intent(inout) :: t(LBi:UBi,LBj:UBj,UBk,3,UBt)
!#endif

!
!  Local variable declarations.
!

      integer :: Iter, i, ibio, itrc, ivar, j, k, ks

      real(r8), parameter :: eps = 1.0e-20_r8

#if defined CARBON || defined OXYGEN
      real(r8) :: u10squ
#endif
#if defined PP_SS
      real(r8) :: sscycle                                  ! annual cycle of primary production (shelf)
      real(r8) :: bbcycle                                  ! annual cycle of primary production (harbour)
#elif defined PP_NWA || defined SOC_NWA
      real(r8) :: fac6, fac7, fac8, fac9, fac10, fac11, fac12, fac13
#endif
#if defined SOC_HRM23 || defined SOC_H2 || defined SOC_H3 || defined SOC_H23
!
! Relationship from SOC in full BGC model
!
      real(r8), parameter :: SOC0 = -0.116199719_r8      ! depth
      real(r8), parameter :: SOC1 = 0.000780643296_r8    ! depth**2
      real(r8), parameter :: SOC2 = -0.00000116530194_r8 ! depth**3
      real(r8), parameter :: SOC3 = -1.07793271_r8       ! lon
      real(r8), parameter :: SOC4 = 1.23783499_r8        ! lat
      real(r8), parameter :: SOC5 = -0.0777753527_r8     ! salt (bottom)
      real(r8), parameter :: SOC6 = 0.554950115_r8       ! salt**2 (bottom)
      real(r8), parameter :: SOC7 = -0.0108994968_r8     ! salt**3 (bottom)
      real(r8), parameter :: SOC8 = -3.34465129_r8       ! temp (bottom)
      real(r8), parameter :: SOC9 = 0.222124249_r8       ! temp**2 (bottom)
      real(r8), parameter :: SOC10 = -0.00413222615_r8   ! temp**3 (bottom)
      real(r8), parameter :: SOC11 = -0.0733407193_r8    ! salt (surface)
      real(r8), parameter :: SOC12 = 0.307952604_r8      ! salt**2 (surface)
      real(r8), parameter :: SOC13 = -0.00764143031_r8   ! salt**2 (surface)
      real(r8), parameter :: SOC14 = 3.59905029_r8       ! temp (surface)
      real(r8), parameter :: SOC15 = -0.307632876_r8     ! temp**2 (surface)
      real(r8), parameter :: SOC16 = 0.00816292443_r8    ! temp**3 (surface)
      real(r8), parameter :: SOC17 = -0.00896775491_r8   ! constant
#elif defined SOC_NWA
      real(r8) :: fyear
      real(r8), parameter, dimension(6) :: SOC0 = (/ 1.31661433_r8,-0.01116848_r8,0.00972495_r8,-0.00039800_r8,0.72632097_r8,-0.23984224_r8/) ! North Shallow
      real(r8), parameter, dimension(6) :: SOC1 = (/ 0.47708264_r8,-0.00377278_r8,0.00155015_r8,-0.00007310_r8,0.03889351_r8, 0.04305900_r8/) ! North Mid depths
      real(r8), parameter, dimension(6) :: SOC2 = (/-0.66599737_r8, 0.00517620_r8,-0.0074901_r8,-0.00081454_r8,1.12531586_r8,-0.37025725_r8/) ! South Shallow
      real(r8), parameter, dimension(6) :: SOC3 = (/-0.52101075_r8, 0.00934503_r8,-0.0046946_r8,-0.00010549_r8,0.22267824_r8,-0.04695529_r8/) ! South Mid depths
#endif
#if defined PP_NWA
#  if !defined SOC_NWA
      real(r8) :: fyear
#  endif
      real(r8), parameter, dimension(8) :: PP0 = (/ 162.49378243_r8, -1.28314680_r8,  1.36252600_r8,  0.09307475_r8,  0.98134401_r8,   -4.27079447_r8,  42.69347396_r8, -14.57635739_r8/) ! North Shallow
      real(r8), parameter, dimension(8) :: PP1 = (/ 156.65324565_r8, -1.82603845_r8,  0.45018415_r8, -0.00298506_r8, -0.98988476_r8,   -1.52062682_r8,  30.75958894_r8,  -8.77117896_r8/) ! North Mid depths
      real(r8), parameter, dimension(8) :: PP2 = (/  89.75440873_r8, -1.81246196_r8, -0.26136077_r8, -0.00028506_r8, 51.47253106_r8, -131.68408246_r8, 147.20939455_r8, -36.33339532_r8/) ! North Deep
      real(r8), parameter, dimension(8) :: PP3 = (/  18.43988431_r8, -1.23164465_r8, -0.62797338_r8,  0.16888450_r8, 24.37651414_r8,   70.43694243_r8, -33.49054017_r8,   2.12852593_r8/) ! South Shallow
      real(r8), parameter, dimension(8) :: PP4 = (/  42.44875611_r8, -1.00834459_r8, -0.27917196_r8, -0.00073139_r8, 32.75467135_r8,   34.53554322_r8,   9.36592999_r8,  -9.99185430_r8/) ! South Mid depths
      real(r8), parameter, dimension(8) :: PP5 = (/  16.68114230_r8,  0.96595887_r8,  0.27357130_r8, -0.00867631_r8, 24.08209930_r8,  -20.81468033_r8,  62.01678867_r8, -22.14601924_r8/) ! South Deep
      real(r8), parameter, dimension(10) :: Z50 = (/ -43.30835056_r8,  0.18009699_r8, -0.04255232_r8, -0.08777949_r8, -1.02904272_r8,    7.09087576_r8,  6.31712577_r8, -11.74699477_r8,  14.29115419_r8 , -4.22924950_r8/) ! North Shallow
      real(r8), parameter, dimension(10) :: Z51 = (/ -65.67687758_r8,  0.17063621_r8, -0.03491390_r8,  0.00050451_r8, -26.54629079_r8, -15.81423291_r8, 62.72956469_r8, -18.15041485_r8, -10.59314266_r8,   3.43511065_r8/) ! North Mid depths
      real(r8), parameter, dimension(10) :: Z52 = (/ -32.03809878_r8, -0.46779175_r8, -0.11352872_r8,  0.00047333_r8,   0.21600975_r8,  22.04537642_r8,  5.87282821_r8, -50.17885862_r8,  57.79302506_r8, -15.17809904_r8/) ! North Deep
      real(r8), parameter, dimension(10) :: Z53 = (/ -53.54144760_r8,  0.13715448_r8, -0.35324706_r8, -0.08349285_r8,   2.47431634_r8,   7.79579078_r8,  3.09589666_r8, -15.96910970_r8,  16.66762929_r8 , -4.43780396_r8/) ! South Shallow
      real(r8), parameter, dimension(10) :: Z54 = (/ -35.76600323_r8, -0.20504031_r8, -0.03834876_r8,  0.00011372_r8,  -9.93681948_r8,  -0.02248369_r8, 18.91068506_r8,  -0.10179996_r8 , -4.82354185_r8,   0.95613896_r8/) ! South Mid depths
      real(r8), parameter, dimension(10) :: Z55 = (/ -37.23545327_r8,  0.10347078_r8,  0.04643287_r8, -0.00269739_r8, -18.39741363_r8,  -4.71055853_r8, 12.29527775_r8,  33.63154985_r8, -31.15131002_r8,   6.76549973_r8/) ! South Deep
#endif
#ifdef OXYGEN
# if defined OCMIP_OXYGEN_SC
!
! Alternative formulation for Schmidt number coefficients (Sc will be
! slightly smaller up to about 35C) using the formulation proposed by
! Keeling et al. (1998, Global Biogeochem. Cycles, 12, 141-163).
!
      real(r8), parameter :: A_O2 = 1638.0_r8
      real(r8), parameter :: B_O2 = 81.83_r8
      real(r8), parameter :: C_O2 = 1.483_r8
      real(r8), parameter :: D_O2 = 0.008004_r8
      real(r8), parameter :: E_O2 = 0.0_r8

# elif defined RW14_OXYGEN_SC
!
! Alternative formulation for Schmidt number coefficients using the
! formulation of Wanninkhof (2014, L and O Methods, 12,351-362).
!
      real(r8), parameter :: A_O2 = 1920.4_r8
      real(r8), parameter :: B_O2 = 135.6_r8
      real(r8), parameter :: C_O2 = 5.2122_r8
      real(r8), parameter :: D_O2 = 0.10939_r8
      real(r8), parameter :: E_O2 = 0.00093777_r8

# else
!
! Schmidt number coefficients using the formulation of
! Wanninkhof (1992).
!
      real(r8), parameter :: A_O2 = 1953.4_r8
      real(r8), parameter :: B_O2 = 128.0_r8
      real(r8), parameter :: C_O2 = 3.9918_r8
      real(r8), parameter :: D_O2 = 0.050091_r8
      real(r8), parameter :: E_O2 = 0.0_r8
#endif
      real(r8), parameter :: OA0 = 2.00907_r8       ! Oxygen
      real(r8), parameter :: OA1 = 3.22014_r8       ! saturation
      real(r8), parameter :: OA2 = 4.05010_r8       ! coefficients
      real(r8), parameter :: OA3 = 4.94457_r8
      real(r8), parameter :: OA4 =-0.256847_r8
      real(r8), parameter :: OA5 = 3.88767_r8
      real(r8), parameter :: OB0 =-0.00624523_r8
      real(r8), parameter :: OB1 =-0.00737614_r8
      real(r8), parameter :: OB2 =-0.0103410_r8
      real(r8), parameter :: OB3 =-0.00817083_r8
      real(r8), parameter :: OC0 =-0.000000488682_r8
      real(r8) :: l2mol = 1000.0_r8/22.3916_r8      ! liter to mol
#endif
#if defined CARBON || defined PP_SS || defined PP_NWA || defined SOC_NWA
      integer :: year
#endif
#ifdef CARBON
      integer, parameter :: DoNewton = 0            ! pCO2 solver
# if defined TALK_ADDITION
      integer :: isink
#  if defined TALK_TWO_FEED
      integer, parameter :: Nsink = 2
#  elif defined TALK_THREE_FEED
      integer, parameter :: Nsink = 3
#  else
      integer, parameter :: Nsink = 1
#  endif
      integer, dimension(Nsink) :: idsink
      real(r8), dimension(Nsink) :: Wbio
# endif

# if defined RW14_CO2_SC
      real(r8), parameter :: A_CO2 = 2116.8_r8      ! Schmidt number
      real(r8), parameter :: B_CO2 = 136.25_r8      ! transfer coeff
      real(r8), parameter :: C_CO2 = 4.7353_r8      ! according to
      real(r8), parameter :: D_CO2 = 0.092307_r8    ! Wanninkhof (2014)
      real(r8), parameter :: E_CO2 = 0.0007555_r8
# else
      real(r8), parameter :: A_CO2 = 2073.1_r8      ! Schmidt
      real(r8), parameter :: B_CO2 = 125.62_r8      ! number
      real(r8), parameter :: C_CO2 = 3.6276_r8      ! transfer
      real(r8), parameter :: D_CO2 = 0.043219_r8    ! coefficients
      real(r8), parameter :: E_CO2 = 0.0_r8
# endif

      real(r8), parameter :: A1 = -60.2409_r8       ! surface
      real(r8), parameter :: A2 = 93.4517_r8        ! CO2
      real(r8), parameter :: A3 = 23.3585_r8        ! solubility
      real(r8), parameter :: B1 = 0.023517_r8       ! coefficients
      real(r8), parameter :: B2 = -0.023656_r8
      real(r8), parameter :: B3 = 0.0047036_r8

      real(r8) :: pmonth                         ! months since Jan 1951
      real(r8) :: pCO2air_secular
      real(dp) :: yday
#ifdef P_PRODUCTION || defined SOC_NWA
      real(r8) :: fac5
#endif
      real(r8), parameter :: pi2 = 6.2831853071796_r8

# if defined PCO2AIR_MAUNALOA
#  if !defined SOC_NWA || !defined PP_NWA
      real(r8) :: fyear                                   ! fractional year
#  endif
      real(r8), parameter :: D0 = 0.338329060_r8
      real(r8), parameter :: D1 = 108.589607716_r8        ! coefficients
      real(r8), parameter :: D2 = 9.440999563_r8          ! to calculate
      real(r8), parameter :: D3 = 2.838323785_r8          ! secular trend in
      real(r8), parameter :: D4 = 0.922577614_r8          ! atmospheric pCO2
      real(r8), parameter :: D5 = 0.999565480_r8
      real(r8), parameter :: D6 = 0.782616227_r8
      real(r8), parameter :: D7 = -157.853151556_r8
      real(r8), parameter :: D8 = 33.919758714_r8
      real(r8), parameter :: D9 = 0.012976532_r8
      real(r8), parameter :: D10 = -50.053077642_r8
      real(r8), parameter :: D11 = 48569.391919959_r8
# elif defined PCO2AIR_SABLEISLAND
#  if !defined SOC_NWA || !defined PP_NWA
      real(r8) :: fyear                                    ! fractional year    
#  endif
      real(r8), parameter :: D0 = 72134.344240476_r8       ! coefficients to calculate
      real(r8), parameter :: D1 = -73.611517390_r8         ! secular trend with a
      real(r8), parameter :: D2 = 0.018864984_r8           ! realistic exponential increase
      real(r8), parameter :: D3 = 5.477205011_r8           ! in atmospheric pCO2
      real(r8), parameter :: D4 = 0.645274882_r8           ! from Sable Island
      real(r8), parameter :: D5 = 1.128926486_r8           ! atm pCO2 data
# endif
#elif defined P_PRODUCTION
      real(dp) :: yday
      real(r8) :: fac5
#endif

      real(r8) :: dtdays

      real(r8) :: cff, cff1, cff2, cff3, cff4, cff5
      real(r8) :: fac1, fac2, fac3, fac4
      real(r8) :: cffL, cffR, cu, dltL, dltR

#ifdef TALK_ADDITION
      real(r8) :: Hadd
#endif

#ifdef TEMP_RATES
      real(r8) :: WOC, WOC2, WOC3
#endif

#ifdef DIAGNOSTICS_BIO
      real(r8) :: fiter
#endif

#ifdef OXYGEN
      real(r8) :: SchmidtN_Ox, O2satu, O2_Flux
      real(r8) :: TS, AA
#endif

#ifdef CARBON
      real(r8) :: CO2c_Flx, CO2a_Flx, CO2_sol, SchmidtN, TempK
#endif

      integer, dimension(IminS:ImaxS,N(ng)) :: ksource

#ifdef CARBON
      real(r8), dimension(IminS:ImaxS) :: pCO2
      real(r8), dimension(IminS:ImaxS) :: pCO2c
# ifdef TALK_ADDITION
      real(r8), dimension(IminS:ImaxS) :: pCO2a
!      real(r8) :: dtsec      ! modified by siyu 2026-12-26
# endif
      real(r8) :: dtsec 
#endif

      real(r8), dimension(IminS:ImaxS,N(ng),NT(ng)) :: Bio
      real(r8), dimension(IminS:ImaxS,N(ng),NT(ng)) :: Bio_old
      
      real(r8), dimension(IminS:ImaxS,0:N(ng)) :: FC

      real(r8), dimension(IminS:ImaxS,N(ng)) :: Hz_inv
      real(r8), dimension(IminS:ImaxS,N(ng)) :: Hz_inv2
      real(r8), dimension(IminS:ImaxS,N(ng)) :: Hz_inv3
      real(r8), dimension(IminS:ImaxS,N(ng)) :: WL
      real(r8), dimension(IminS:ImaxS,N(ng)) :: WR
      real(r8), dimension(IminS:ImaxS,N(ng)) :: bL
      real(r8), dimension(IminS:ImaxS,N(ng)) :: bR
      real(r8), dimension(IminS:ImaxS,N(ng)) :: qc

!-----------------------------------------------------------------------------------------
! Coefficients for temperature dependent rates 
! Temperature dependency of the form: parameter(ng)*Tcoef0*(Tcoef1**Bio(i,k,itemp))
!-----------------------------------------------------------------------------------------
!
!  (Eppley, R.W.,1972, Fishery Bulletin, 70: 1063-1085; growth rate
!  provided in doublings per day. Here 0.59=ln(2)*0.851, i.e., growth rate
!  per day is equal to ln(2)xdoubling per day).

       real(r8), parameter :: Tcoef0 = 0.59_r8
       real(r8), parameter :: Tcoef1 = 1.066_r8

#include "set_bounds.h"
#ifdef DIAGNOSTICS_BIO
!
!-----------------------------------------------------------------------
! If appropriate, initialize time-averaged diagnostic arrays.
!-----------------------------------------------------------------------
!
      IF (((iic(ng).gt.ntsDIA(ng)).and.                                 &
     &     (MOD(iic(ng),nDIA(ng)).eq.1)).or.                            &
     &    ((iic(ng).ge.ntsDIA(ng)).and.(nDIA(ng).eq.1)).or.             &
     &    ((nrrec(ng).gt.0).and.(iic(ng).eq.ntstart(ng)))) THEN
        DO ivar=1,NDbio2d
          DO j=Jstr,Jend
            DO i=Istr,Iend
              DiaBio2d(i,j,ivar)=0.0_r8
            END DO
          END DO
        END DO
      END IF
#endif
!
!-----------------------------------------------------------------------
!  Add biological Source/Sink terms.
!-----------------------------------------------------------------------
!
!  Avoid computing source/sink terms if no biological iterations.
!
      IF (BioIter(ng).le.0) RETURN
!
!  Set time-stepping according to the number of iterations.
!
      dtdays=dt(ng)*sec2day/REAL(BioIter(ng),r8)
      dtsec=dt(ng)/REAL(BioIter(ng),r8)
#ifdef DIAGNOSTICS_BIO
!
!  A factor to account for the number of iterations in accumulating
!  diagnostic rate variables.
!
      fiter=1.0_r8/REAL(BioIter(ng),r8)
#endif

#ifdef PP_SS
      CALL caldate (tdays(ng), yy_i=year, yd_dp=yday)
      sscycle=0.15_r8+0.96_r8*((COS(0.00834_r8*yday-1.0_r8)             &
     &        +1.0_r8)**24.0_r8/16777141.0_r8+(SIN(0.01428_r8*yday      &
     &        -1.0_r8)+1.0_r8)**24.0_r8/55923804.0_r8                   &
     &        +(SIN(0.01074_r8*yday-1.0_r8)                             &
     &        +1.0_r8)**24.0_r8/27961902.0_r8                           &
     &        +(SIN(0.00867_r8*yday-1.0_r8)+1.0_r8)**24/87961902.0_r8)

      bbcycle=0.15_r8+4.0_r8*((COS(0.00834_r8*yday-1.0_r8)              &
     &        +1.0_r8)**24.0_r8/16777141.0_r8+(SIN(0.01282_r8*yday      &
     &        -1.0_r8)+1.0_r8)**24.0_r8/45923804.0_r8                   &
     &        +(SIN(0.00945_r8*yday-1.0_r8)                             &
     &        +1.0_r8)**24.0_r8/27961902.0_r8                           &
     &        +(SIN(0.00867_r8*yday-1.0_r8)+1.0_r8)**24/87961902.0_r8)
#elif defined PP_NWA || defined SOC_NWA
      CALL caldate (tdays(ng), yy_i=year, yd_dp=yday)
      fyear=year+MIN((yday-1.0_r8)/365.0_r8,1.0_r8)
      fac6=SIN(pi2/2*fyear)
      fac7=SIN(pi2*fyear)**2
      fac8=MAX(SIN(pi2*fyear)**3,0.0_r8)
      fac9=MAX(-SIN(pi2*fyear)**3,0.0_r8)
      fac10=1.0_r8-COS(pi2*fyear)      ! fyear_cos1 = 1-np.cos(doy/365*2*np.pi)
      fac11=(1.0_r8-COS(pi2*fyear))**2 ! fyear_cos2 = (1-np.cos(doy/365*2*np.pi))**2
      fac12=(1.0_r8-COS(pi2*fyear))**3 ! fyear_cos3 = (1-np.cos(doy/365*2*np.pi))**3
      fac13=(1.0_r8-COS(pi2*fyear))**4 ! fyear_cos4 = (1-np.cos(doy/365*2*np.pi))**4
#endif

# ifdef TALK_ADDITION
!
!  Set vertical sinking indentification.
!
      idsink(1)=iTAp1
#  if defined TALK_TWO_FEED || defined TALK_THREE_FEED
      idsink(2)=iTAp2
#  endif
#  ifdef TALK_THREE_FEED
      idsink(3)=iTAp3
#  endif

!
!  Set vertical sinking velocity vector in the same order as the
!  identification vector, IDSINK.
!
      Wbio(1)=wTAp(1,ng)       ! sinking velocity of particulate feedstock (TAp1)
#  if defined TALK_TWO_FEED || defined TALK_THREE_FEED
      Wbio(2)=wTAp(2,ng)       ! sinking velocity of particulate feedstock 2 (TAp2)
#  endif
#  ifdef TALK_THREE_FEED
      Wbio(3)=wTAp(3,ng)       ! sinking velocity of particulate feedstock 3 (TAp3)
#  endif
# endif

!
!  Compute inverse thickness to avoid repeated divisions.
!
      J_LOOP : DO j=Jstr,Jend
        DO k=1,N(ng)
          DO i=Istr,Iend
            Hz_inv(i,k)=1.0_r8/Hz(i,j,k)
          END DO
        END DO
        DO k=1,N(ng)-1
          DO i=Istr,Iend
            Hz_inv2(i,k)=1.0_r8/(Hz(i,j,k)+Hz(i,j,k+1))
          END DO
        END DO
        DO k=2,N(ng)-1
          DO i=Istr,Iend
            Hz_inv3(i,k)=1.0_r8/(Hz(i,j,k-1)+Hz(i,j,k)+Hz(i,j,k+1))
          END DO
        END DO
!
!  Extract biological variables from tracer arrays, place them into
!  scratch arrays, and restrict their values to be positive definite.
!  At input, all tracers (index nnew) from predictor step have
!  transport units (m Tunits) since we do not have yet the new
!  values for zeta and Hz. These are known after the 2D barotropic
!  time-stepping.
!
        DO itrc=1,NBT
          ibio=idbio(itrc)
          DO k=1,N(ng)
            DO i=Istr,Iend
              Bio_old(i,k,ibio)=MAX(0.0_r8,t(i,j,k,nstp,ibio))
              Bio(i,k,ibio)=Bio_old(i,k,ibio)
            END DO
          END DO
        END DO
#ifdef CARBON
        DO k=1,N(ng)
          DO i=Istr,Iend
            Bio_old(i,k,iTIC_)=MIN(Bio_old(i,k,iTIC_),3000.0_r8)
            Bio_old(i,k,iTIC_)=MAX(Bio_old(i,k,iTIC_),400.0_r8)
            Bio(i,k,iTIC_)=Bio_old(i,k,iTIC_)
          END DO
        END DO
#endif
!
!  Extract potential temperature and salinity.
!
        DO k=1,N(ng)
          DO i=Istr,Iend
            Bio(i,k,itemp)=MIN(t(i,j,k,nstp,itemp),35.0_r8)
            Bio(i,k,isalt)=MAX(t(i,j,k,nstp,isalt), 0.0_r8)
          END DO
        END DO
!
!=======================================================================
!  Start internal iterations to achieve convergence of the nonlinear
!  backward-implicit solution.
!=======================================================================
!
!  During the iterative procedure a series of fractional time steps are
!  performed in a chained mode (splitting by different biological
!  conversion processes) in sequence of the main food chain.  In all
!  stages the concentration of the component being consumed is treated
!  in fully implicit manner, so the algorithm guarantees non-negative
!  values, no matter how strong s the concentration of active consuming
!  component (Phytoplankton or Zooplankton).  The overall algorithm,
!  as well as any stage of it, is formulated in conservative form
!  (except explicit sinking) in sense that the sum of concentration of
!  all components is conserved.
!
!
!  In the implicit algorithm, we have for example (N: nitrate,
!                                                  P: phytoplankton),
!
!     N(new) = N(old) - uptake * P(old)     uptake = mu * N / (Kn + N)
!                                                    {Michaelis-Menten}
!  below, we set
!                                           The N in the numerator of
!     cff = mu * P(old) / (Kn + N(old))     uptake is treated implicitly
!                                           as N(new)
!
!  so the time-stepping of the equations becomes:
!
!     N(new) = N(old) / (1 + cff)     (1) when substracting a sink term,
!                                         consuming, divide by (1 + cff)
!  and
!
!     P(new) = P(old) + cff * N(new)  (2) when adding a source term,
!                                         growing, add (cff * source)
!
!  Notice that if you substitute (1) in (2), you will get:
!
!     P(new) = P(old) + cff * N(old) / (1 + cff)    (3)
!
!  If you add (1) and (3), you get
!
!     N(new) + P(new) = N(old) + P(old)
!
!  implying conservation regardless how "cff" is computed. Therefore,
!  this scheme is unconditionally stable regardless of the conversion
!  rate. It does not generate negative values since the constituent
!  to be consumed is always treated implicitly. It is also biased
!  toward damping oscillations.
!
!  The iterative loop below is to iterate toward an universal Backward-
!  Euler treatment of all terms. So if there are oscillations in the
!  system, they are only physical oscillations. These iterations,
!  however, do not improve the accuaracy of the solution.
!
        ITER_LOOP: DO Iter=1,BioIter(ng)

#ifdef TEMP_RATES
          WOC=WOC0(ng)*dtdays
# if defined WOC_HRM23 || defined WOC_H2 || defined WOC_H3 || defined WOC_H23
          WOC2=WOC20(ng)*dtdays
          WOC3=WOC30(ng)*dtdays
# endif
#else
          fac1=dtdays*WOC(ng)
# if defined WOC_HRM23 || defined WOC_H2 || defined WOC_H3 || defined WOC_H23
          fac3=dtdays*WOC2(ng)
          fac4=dtdays*WOC3(ng)
# endif
#endif

#ifdef TALK_ADDITION
!-----------------------------------------------------------------------
!  Get thickness of addition layer
!-----------------------------------------------------------------------
# if !defined TALK_FILE
          IF (j.eq.jloc_alkalinity(ng)) THEN
            Hadd=SUM(Hz(iloc_alkalinity(ng),jloc_alkalinity(ng),        &
     &                kloc_alkalinity_min(ng):kloc_alkalinity_max(ng)))
          END IF
# endif
#endif

          DO k=1,N(ng)
            DO i=Istr,Iend

#ifdef TEMP_RATES
          fac1=WOC*Tcoef0*(Tcoef1**Bio(i,k,itemp))
# if defined WOC_HRM23 || defined WOC_H2 || defined WOC_H3 || defined WOC_H23
          fac3=WOC2*Tcoef0*(Tcoef1**Bio(i,k,itemp))
          fac4=WOC3*Tcoef0*(Tcoef1**Bio(i,k,itemp))
# endif
#endif
!
!-----------------------------------------------------------------------
!  Compute primary production from relationship (function of z and temp)
!-----------------------------------------------------------------------
!
#ifdef P_PRODUCTION
          fac2=0.0_r8
# if defined PP_SS
#  ifdef PP_HRM23
              IF (((ng .eq. 1) .and. (i .lt. 51) .and. (j .gt. 247))    &
     &                 .or. (ng .eq. 2)) THEN
#  elif defined PP_H2
              IF ((ng .eq. 1) .and. ((j .gt. 340) .or. ((i .gt. 135)    &
     &                .and. j .gt. 320 ))) THEN
#  elif defined PP_H3
              IF ((ng .eq. 1) .and. ((j .gt. 150) .or. ((i .gt. 50)     &
     &                .and. j .gt. 92 ))) THEN
#  elif defined PP_H23
              IF (((ng .eq. 1) .and. ((j .gt. 340) .or. ((i .gt. 135)   &
     &                 .and. j .gt. 320 ))) .or.                        &
                  ((ng .eq. 2) .and. ((j .gt. 150) .or. ((i .gt. 50)    &
     &                 .and. j .gt. 92 )))) THEN
#  else
              IF (ng .eq. 0) THEN
#  endif
                IF ((z_r(i,j,k) .gt. -15.0_r8) .and.                    &
     &             (z_w(i,j,0) .lt. -6.0_r8) .and. (j .lt. 390)) THEN
          fac2=bbcycle
                END IF
              ELSE
                IF ((z_r(i,j,k) .gt. -30.0_r8) .and.                    &
     &             (z_w(i,j,0) .lt. -6.0_r8)) THEN
          fac2=sscycle
                END IF
               END IF
# elif defined PP_NWA
            cff5=ABS(z_w(i,j,0))
            IF ((latr(i,j) .ge. 53.45_r8) .or.                          &
     &            ((latr(i,j) .ge. 50_r8) .and.                         &
     &            (lonr(i,j) .le. -76_r8))) THEN      ! Northern area
                IF ((cff5 .lt. 200_r8)) THEN        ! Shef area
                  cff2=PP0(1)+PP0(2)*latr(i,j)+PP0(3)*lonr(i,j)+        &
     &                 PP0(4)*cff5+PP0(5)*fac10+PP0(6)*fac11+           &
     &                 PP0(7)*fac12+PP0(8)*fac13
                  cff3=Z50(1)+Z50(2)*latr(i,j)+Z50(3)*lonr(i,j)+        &
     &                 Z50(4)*cff5+Z50(5)*fac8+Z50(6)*fac9+Z50(7)*fac10+&
     &                 Z50(8)*fac11+Z50(9)*fac12+Z50(10)*fac13
                ELSEIF ((cff5 .lt. 3000_r8)) THEN   ! Slope area
                  cff2=PP1(1)+PP1(2)*latr(i,j)+PP1(3)*lonr(i,j)+        &
     &                 PP1(4)*cff5+PP1(5)*fac10+PP1(6)*fac11+           &
     &                 PP1(7)*fac12+PP1(8)*fac13
                  cff3=Z51(1)+Z51(2)*latr(i,j)+Z51(3)*lonr(i,j)+        &
     &                 Z51(4)*cff5+Z51(5)*fac8+Z51(6)*fac9+Z51(7)*fac10+&
     &                 Z51(8)*fac11+Z51(9)*fac12+Z51(10)*fac13
                ELSE                                ! Deep area
                  cff2=PP2(1)+PP2(2)*latr(i,j)+PP2(3)*lonr(i,j)+        &
     &                 PP2(4)*cff5+PP2(5)*fac10+PP2(6)*fac11+           &
     &                 PP2(7)*fac12+PP2(8)*fac13
                  cff3=Z52(1)+Z52(2)*latr(i,j)+Z52(3)*lonr(i,j)+        &
     &                 Z52(4)*cff5+Z52(5)*fac8+Z52(6)*fac9+Z52(7)*fac10+&
     &                 Z52(8)*fac11+Z52(9)*fac12+Z52(10)*fac13
                END IF
            ELSE                   ! Southern area
                IF ((cff5 .lt. 200_r8)) THEN        ! Shef area
                  cff2=PP3(1)+PP3(2)*latr(i,j)+PP3(3)*lonr(i,j)+        &
     &                 PP3(4)*cff5+PP3(5)*fac10+PP3(6)*fac11+           &
     &                 PP3(7)*fac12+PP3(8)*fac13
                  cff3=Z53(1)+Z53(2)*latr(i,j)+Z53(3)*lonr(i,j)+        &
     &                 Z53(4)*cff5+Z53(5)*fac8+Z53(6)*fac9+Z53(7)*fac10+&
     &                 Z53(8)*fac11+Z53(9)*fac12+Z53(10)*fac13
                ELSEIF ((cff5 .lt. 3000_r8)) THEN   ! Slope area
                  cff2=PP4(1)+PP4(2)*latr(i,j)+PP4(3)*lonr(i,j)+        &
     &                 PP4(4)*cff5+PP4(5)*fac10+PP4(6)*fac11+           &
     &                 PP4(7)*fac12+PP4(8)*fac13
                  cff3=Z54(1)+Z54(2)*latr(i,j)+Z54(3)*lonr(i,j)+        &
     &                 Z54(4)*cff5+Z54(5)*fac8+Z54(6)*fac9+Z54(7)*fac10+&
     &                 Z54(8)*fac11+Z54(9)*fac12+Z54(10)*fac13
                ELSE                                ! Deep area
                  cff2=PP5(1)+PP5(2)*latr(i,j)+PP5(3)*lonr(i,j)+        &
     &                 PP5(4)*cff5+PP5(5)*fac10+PP5(6)*fac12+           &
     &                 PP5(7)*fac12+PP5(8)*fac13
                  cff3=Z55(1)+Z55(2)*latr(i,j)+Z55(3)*lonr(i,j)+        &
     &                 Z55(4)*cff5+Z55(5)*fac8+Z55(6)*fac9+Z55(7)*fac10+&
     &                 Z55(8)*fac11+Z55(9)*fac12+Z55(10)*fac13
                END IF
            END IF
            cff3=MIN(cff3,0.0_r8)
            fac2=0.0_r8
            IF ((z_w(i,j,k) .gt. cff3)) THEN
               IF ((cff3 .lt. -5.0_r8)) THEN
                  fac2=MAX(cff2,0.0_r8)/ABS(cff3)
               END IF
            END IF
# else
          fac2=7.7975857554_r8+0.3419993856_r8*(-z_r(i,j,k))+           &
     &                          0.0034178070_r8*z_r(i,j,k)**2+          &
     &                          0.0151537941_r8*Bio(i,k,itemp)**2
# endif
#else
          fac2=0.0_r8
#endif
!
!-----------------------------------------------------------------------
!  Primary production
!-----------------------------------------------------------------------
!
          cff1=dtdays*MAX(fac2,0.0_r8)
#ifdef OXYGEN
              Bio(i,k,iOxyg)=Bio(i,k,iOxyg)+cff1
#endif
#ifdef CARBON
              Bio(i,k,iTIC_)=Bio(i,k,iTIC_)-cff1
#endif

!
!-----------------------------------------------------------------------
! Water column respiration
!-----------------------------------------------------------------------
!
#if defined  WOC_HRM23 || defined WOC_H2 || defined WOC_H3 || defined WOC_H23
            IF ((z_w(i,j,0) .lt. -6.0_r8)) THEN
# if defined WOC_HRM23
              IF (((ng .eq. 1) .and. (i .lt. 51) .and. (j .gt. 247))    &
     &                 .or. (ng .eq. 2)) THEN
# elif defined WOC_H2
              IF ((ng .eq. 1) .and. ((j .gt. 340) .or. ((i .gt. 135)    &
     &                 .and. j .gt. 320 ))) THEN
# elif defined WOC_H3
              IF ((ng .eq. 1) .and. ((j .gt. 150) .or. ((i .gt. 50)     &
     &                 .and. j .gt. 92 ))) THEN
# elif defined WOC_H23
              IF (((ng .eq. 1) .and. ((j .gt. 340) .or. ((i .gt. 135)   &
     &                 .and. j .gt. 320 ))) .or.                        &
                  ((ng .eq. 2) .and. ((j .gt. 150) .or. ((i .gt. 50)    &
     &                 .and. j .gt. 92 )))) THEN
# else
              IF ((ng .eq. 0)) THEN
# endif
# if defined WOC_H2
                IF ((j .lt. 390)) THEN
# elif defined WOC_H3
                IF ((j .lt. 302)) THEN
# elif defined WOC_H23
                IF (((ng .eq. 1) .and. (j .lt. 390)) .or.               &
                        ((ng .eq. 2) .and. (j .lt. 302))) THEN
# endif
# ifdef OXYGEN
              Bio(i,k,iOxyg)=Bio(i,k,iOxyg)-fac4
# endif
# ifdef CARBON
              Bio(i,k,iTIC_)=Bio(i,k,iTIC_)+fac4
#  ifdef TALK_NONCONSERV
#   ifdef TALK_BGC
              Bio(i,k,iTAlk)=Bio(i,k,iTAlk)+fac4*PhyNC(ng)
#   endif
#  endif
# endif
# if defined WOC_H2 || defined WOC_H3 || defined WOC_H23
                END IF
# endif
              ELSE
# ifdef OXYGEN
              Bio(i,k,iOxyg)=Bio(i,k,iOxyg)-fac3
# endif
# ifdef CARBON
              Bio(i,k,iTIC_)=Bio(i,k,iTIC_)+fac3
#  ifdef TALK_NONCONSERV
#   ifdef TALK_BGC
              Bio(i,k,iTAlk)=Bio(i,k,iTAlk)+fac3*PhyNC(ng)
#   endif
#  endif
# endif
              END IF
            END IF
#else
# ifdef OXYGEN
              Bio(i,k,iOxyg)=Bio(i,k,iOxyg)-fac1
# endif
# ifdef CARBON
              Bio(i,k,iTIC_)=Bio(i,k,iTIC_)+fac1
#  ifdef TALK_NONCONSERV
#   ifdef TALK_BGC
              Bio(i,k,iTAlk)=Bio(i,k,iTAlk)+fac1*PhyNC(ng)
#   endif
#  endif
# endif
#endif

#ifdef TALK_ADDITION
!
!-----------------------------------------------------------------------
!  Compute dissolution of particles (Implicit formulation)
!  dTAp_dt = -K*TAp
!         dTA_dt =  K*TAp
!-----------------------------------------------------------------------
!
               fac1=MAX(Bio(i,k,iTAp1),0.0_r8)
               cff4=dtdays*dissTAp(1,ng)
               IF (k.eq.1) THEN
                 cff5=1.0_r8-sedloss(ng)
               ELSE
                 cff5=1.0_r8
               END IF
               Bio(i,k,iTAp1)=fac1/(1+cff4)
               Bio(i,k,idTA)=Bio(i,k,idTA)+cff4*cff5*                   &
     &                                     Bio(i,k,iTAp1)
# ifdef TALK_DIAG_DISS
               Bio(i,k,iTArm)=Bio(i,k,iTArm)+cff4*cff5*                 &
     &                                       Bio(i,k,iTAp1)
# endif
# ifdef TALK_TWO_FEED
               fac1=MAX(Bio(i,k,iTAp2),0.0_r8)
               cff4=dtdays*dissTAp(2,ng)
               Bio(i,k,iTAp2)=fac1/(1+cff4)
               Bio(i,k,idTA)=Bio(i,k,idTA)+cff4*cff5*                   &
     &                                     Bio(i,k,iTAp2)
#  ifdef TALK_DIAG_DISS
               Bio(i,k,iTArm)=Bio(i,k,iTArm)+cff4*cff5*                 &
     &                                       Bio(i,k,iTAp2)
#  endif
# endif
# ifdef TALK_THREE_FEED
               fac1=MAX(Bio(i,k,iTAp2),0.0_r8)
               cff4=dtdays*dissTAp(2,ng)
               Bio(i,k,iTAp2)=fac1/(1+cff4)
               Bio(i,k,idTA)=Bio(i,k,idTA)+cff4*cff5*                   &
     &                                     Bio(i,k,iTAp2)
#  ifdef TALK_DIAG_DISS
               Bio(i,k,iTArm)=Bio(i,k,iTArm)+cff4*cff5*                 &
     &                                       Bio(i,k,iTAp2)
#  endif
               fac1=MAX(Bio(i,k,iTAp3),0.0_r8)
               cff4=dtdays*dissTAp(3,ng)
               Bio(i,k,iTAp3)=fac1/(1+cff4)
               Bio(i,k,idTA)=Bio(i,k,idTA)+cff4*cff5*                   &
     &                                     Bio(i,k,iTAp3)
#  ifdef TALK_DIAG_DISS
               Bio(i,k,iTArm)=Bio(i,k,iTArm)+cff4*cff5*                 &
     &                                       Bio(i,k,iTAp3)
#  endif
# endif
!
!-----------------------------------------------------------------------
!  Compute external source of alkalinity (mol/second)
!-----------------------------------------------------------------------
!
               cff1=0.0_r8
               cff=0.0_r8
# ifdef TALK_FILE
! Add Alkalinity from river file
              IF ((taflx(i,j).gt.0.0_r8)                                &
#  if defined TALK_TWO_FEED
     &          .and. (tatype(i,j).lt.3.0_r8)                           &
#  elif !defined TALK_THREE_FEED
     &          .and. (tatype(i,j).lt.2.0_r8)                           &
#  endif
     &          .and. (k.ge.takmin(i,j)) .and. (k.le.takmax(i,j))) THEN
               cff=pm(i,j)*pn(i,j)
               Hadd=SUM(Hz(i,j,takmin(i,j):takmax(i,j)))
               cff1=taflx(i,j)
# else
! Alkalinity flux (constant) is defined in reduced_bgc.in
              IF (i.eq.iloc_alkalinity(ng)                              &
     &          .and. j.eq.jloc_alkalinity(ng)                          &
     &          .and. k.ge.kloc_alkalinity_min(ng)                      &
     &          .and. k.le.kloc_alkalinity_max(ng)                      &
     &          .and. tdays(ng) .ge. alkalinity_startload(ng)           &
     &          .and. tdays(ng) .le. alkalinity_endload(ng)) THEN
                 cff=pm(i,j)*pn(i,j)
                 cff1=alkalinity_load(ng)
# endif
               cff2=cff1*1000.0_r8*dtsec*cff/Hadd !*Hz(i,j,k)/Hadd
# ifndef TALK_FILE
               cff3=(1-P2Dratio(1,ng))*cff2
               Bio(i,k,idTA)=Bio(i,k,idTA)+cff3
               Bio(i,k,iTAp1)=Bio(i,k,iTAp1)+P2Dratio(1,ng)*cff2
# else
               cff3=(1-P2Dratio(INT(tatype(i,j)),ng))*cff2
               Bio(i,k,idTA)=Bio(i,k,idTA)+cff3
#  if defined TALK_TWO_FEED
                IF (tatype(i,j).eq.1.0_r8) THEN
               Bio(i,k,iTAp1)=Bio(i,k,iTAp1)+P2Dratio(1,ng)*cff2
                ELSE IF (tatype(i,j).eq.2.0_r8) THEN
               Bio(i,k,iTAp2)=Bio(i,k,iTAp2)+P2Dratio(2,ng)*cff2
                END IF
#  elif defined TALK_THREE_FEED
                IF (tatype(i,j).eq.1.0_r8) THEN
               Bio(i,k,iTAp1)=Bio(i,k,iTAp1)+P2Dratio(1,ng)*cff2
                ELSE IF (tatype(i,j).eq.2.0_r8) THEN
               Bio(i,k,iTAp2)=Bio(i,k,iTAp2)+P2Dratio(2,ng)*cff2
                ELSE IF (tatype(i,j).eq.3.0_r8) THEN
               Bio(i,k,iTAp3)=Bio(i,k,iTAp3)+P2Dratio(3,ng)*cff2
                END IF
#  else
               Bio(i,k,iTAp1)=Bio(i,k,iTAp1)+P2Dratio(1,ng)*cff2
#  endif
# endif
              END IF
#endif
            END DO
          END DO
#ifdef OXYGEN
!
!-----------------------------------------------------------------------
!  Surface O2 gas exchange.
!-----------------------------------------------------------------------
!
!  Compute surface O2 gas exchange.
!
          cff1=rho0*550.0_r8
# if defined RW14_OXYGEN_SC
          cff2=dtdays*0.251_r8*24.0_r8/100.0_r8
# else
          cff2=dtdays*0.31_r8*24.0_r8/100.0_r8
# endif
          k=N(ng)
          DO i=Istr,Iend
!
!  Compute O2 transfer velocity : u10squared (u10 in m/s)
!
# ifdef BULK_FLUXES
            u10squ=Uwind(i,j)*Uwind(i,j)+Vwind(i,j)*Vwind(i,j)
# else
            u10squ=cff1*SQRT((0.5_r8*(sustr(i,j)+sustr(i+1,j)))**2+     &
     &                       (0.5_r8*(svstr(i,j)+svstr(i,j+1)))**2)
# endif
            SchmidtN_Ox=A_O2-Bio(i,k,itemp)*(B_O2-Bio(i,k,itemp)*(C_O2- &
     &                                            Bio(i,k,itemp)*(D_O2- &
     &                                            Bio(i,k,itemp)*E_O2)))
            cff3=cff2*u10squ*SQRT(660.0_r8/SchmidtN_Ox)
!
!  Calculate O2 saturation concentration using Garcia and Gordon
!  L and O (1992) formula, (EXP(AA) is in ml/l).
!
            TS=LOG((298.15_r8-Bio(i,k,itemp))/                          &
     &             (273.15_r8+Bio(i,k,itemp)))
            AA=OA0+TS*(OA1+TS*(OA2+TS*(OA3+TS*(OA4+TS*OA5))))+          &
     &             Bio(i,k,isalt)*(OB0+TS*(OB1+TS*(OB2+TS*OB3)))+       &
     &             OC0*Bio(i,k,isalt)*Bio(i,k,isalt)
!
!  Convert from ml/l to mmol/m3.
!
            O2satu=l2mol*EXP(AA)
!
!  Add in O2 gas exchange.
!
            O2_Flux=cff3*(O2satu-Bio(i,k,iOxyg))
            Bio(i,k,iOxyg)=Bio(i,k,iOxyg)+                              &
     &                     O2_Flux*Hz_inv(i,k)
# ifdef DIAGNOSTICS_BIO
            DiaBio2d(i,j,iO2fx)=DiaBio2d(i,j,iO2fx)+                    &
#  ifdef WET_DRY
     &                          rmask_full(i,j)*                        &
#  endif
     &                          O2_Flux*fiter
# endif

          END DO
#endif

#ifdef CARBON
          DO k=1,N(ng)
            DO i=Istr,Iend
# ifndef TALK_NONCONSERV
!
!  Alkalinity is treated as a diagnostic variable. TAlk = f(S[PSU])
!
!  Relationship from Brewer et al. (1986).
              Bio(i,k,iTAlk)=587.05_r8+50.56_r8*Bio(i,k,isalt)
# endif
            END DO
          END DO
!
!-----------------------------------------------------------------------
!  Surface CO2 gas exchange.
!-----------------------------------------------------------------------
!
!  Compute equilibrium partial pressure inorganic carbon (ppmv) at the
!  surface.
!
          k=N(ng)
          DO i=Istr,Iend  
            pH(i,j)=pHc(i,j)  
            pCO2(i)=pCO2c(i)  
          END DO  
# ifdef pCO2_RZ
          CALL pCO2_water_RZ (Istr, Iend, LBi, UBi, LBj, UBj,           &
     &                        IminS, ImaxS, j, DoNewton,                &
#  ifdef MASKING
     &                        rmask,                                    &
#  endif
     &                        Bio(IminS:,k,itemp), Bio(IminS:,k,isalt), &
     &                        Bio(IminS:,k,iTIC_), Bio(IminS:,k,iTAlk), &
     &                        pH, pCO2)

          DO i=Istr,Iend  
            pHc(i,j)=pH(i,j)  
            pCO2c(i)=pCO2(i)  
          END DO

#  if defined TALK_ADDITION
          pH(IminS:,j)=pHa(IminS:,j)
          pCO2(IminS:)=pCO2a(IminS:)
          CALL pCO2_water_RZ (Istr, Iend, LBi, UBi, LBj, UBj,           &
     &                        Istr, Iend, j, DoNewton,                &
#   ifdef MASKING
     &                        rmask,                                    &
#   endif
     &                        Bio(IminS:,k,itemp), Bio(IminS:,k,isalt), &
     &                        Bio(IminS:,k,iTIC_)+Bio(IminS:,k,idTIC),  &
     &                        Bio(IminS:,k,iTAlk)+Bio(IminS:,k,idTA),   &
     &                        pH, pCO2)
          pHa(IminS:,j)=pH(IminS:,j)
          pCO2a(IminS:)=pCO2(IminS:)
#  endif
# else
          CALL pCO2_water (Istr, Iend, LBi, UBi, LBj, UBj,              &
     &                     Istr, Iend, j, DoNewton,                   &
#  ifdef MASKING
     &                     rmask,                                       &
#  endif
     &                     Bio(IminS:,k,itemp), Bio(IminS:,k,isalt),    &
     &                     Bio(IminS:,k,iTIC_), Bio(IminS:,k,iTAlk),    &
     &                     0.0_r8, 0.0_r8, pH, pCO2)

          DO i=Istr,Iend  
            pHc(i,j)=pH(i,j)  
            pCO2c(i)=pCO2(i)  
          END DO

#  if defined TALK_ADDITION
          pH(IminS:,j)=pHa(IminS:,j)
          pCO2(IminS:)=pCO2a(IminS:)
          CALL pCO2_water (Istr, Iend, LBi, UBi, LBj, UBj,              &
     &                     IminS, ImaxS, j, DoNewton,                   &
#  ifdef MASKING
     &                     rmask,                                       &
#  endif
     &                     Bio(IminS:,k,itemp), Bio(IminS:,k,isalt),    &
     &                     Bio(IminS:,k,iTIC_)+Bio(IminS:,k,idTIC),     &
     &                     Bio(IminS:,k,iTAlk)+Bio(IminS:,k,idTA),      &
     &                     0.0_r8, 0.0_r8, pH, pCO2)
          pHa(IminS:,j)=pH(IminS:,j)
          pCO2a(IminS:)=pCO2(IminS:)
#  endif
# endif
!
!  Compute surface CO2 gas exchange.
!
          cff1=rho0*550.0_r8
# if defined RW14_CO2_SC
          cff2=dtdays*0.251_r8*24.0_r8/100.0_r8
# else
          cff2=dtdays*0.31_r8*24.0_r8/100.0_r8
# endif
          DO i=Istr,Iend
!
!  Compute CO2 transfer velocity : u10squared (u10 in m/s)
!
# ifdef BULK_FLUXES
            u10squ=Uwind(i,j)**2+Vwind(i,j)**2
# else
            u10squ=cff1*SQRT((0.5_r8*(sustr(i,j)+sustr(i+1,j)))**2+     &
     &                       (0.5_r8*(svstr(i,j)+svstr(i,j+1)))**2)
# endif
            SchmidtN=A_CO2-Bio(i,k,itemp)*(B_CO2-Bio(i,k,itemp)*(C_CO2- &
     &                                           Bio(i,k,itemp)*(D_CO2- &
     &                                           Bio(i,k,itemp)*E_CO2)))
            cff3=cff2*u10squ*SQRT(660.0_r8/SchmidtN)
!
!  Calculate CO2 solubility [mol/(kg.atm)] using Weiss (1974) formula.
!
            TempK=0.01_r8*(Bio(i,k,itemp)+273.15_r8)
            CO2_sol=EXP(A1+                                             &
     &                  A2/TempK+                                       &
     &                  A3*LOG(TempK)+                                  &
     &                  Bio(i,k,isalt)*(B1+TempK*(B2+B3*TempK)))
!
!  Add in CO2 gas exchange.
!
            CALL caldate (tdays(ng), yy_i=year, yd_dp=yday)
            pmonth=year-1951.0_r8+yday/365.0_r8
# if defined PCO2AIR_DATA
            pCO2air_secular=380.464_r8+9.321_r8*SIN(pi2*yday/365.25_r8+ &
     &                      1.068_r8)
            CO2c_Flx=cff3*CO2_sol*(pCO2air_secular-pCO2c(i))
#   if defined TALK_ADDITION
            CO2a_Flx=cff3*CO2_sol*(pCO2air_secular-pCO2a(i))
#   endif
# elif defined PCO2AIR_MAUNALOA
            fyear=year+MIN((yday-1.0_r8)/365.0_r8,1.0_r8)
            pCO2air_secular=D0*SIN(pi2*(fyear-D1)/D2)+                  &
     &                      D3*SIN(pi2*(fyear-D4)/D5)+                  &
     &                      D6*SIN(pi2*(fyear-D7)/D8)+                  &
     &                      D9*fyear**2+D10*fyear+D11
            CO2c_Flx=cff3*CO2_sol*(pCO2air_secular-pCO2c(i))
#   if defined TALK_ADDITION
            CO2a_Flx=cff3*CO2_sol*(pCO2air_secular-pCO2a(i))
#   endif
# elif defined PCO2AIR_SABLEISLAND
            fyear=year+MIN((yday-1.0_r8)/365.0_r8,1.0_r8)
            pCO2air_secular=D0+D1*fyear+D2*fyear**2+                    &
     &                      D3*1.25_r8*LOG(SIN(pi2*fyear+D4)+D5)
            CO2c_Flx=cff3*CO2_sol*(pCO2air_secular-pCO2c(i))
#   if defined TALK_ADDITION
            CO2a_Flx=cff3*CO2_sol*(pCO2air_secular-pCO2a(i))
#   endif
# else
            CO2c_Flx=cff3*CO2_sol*(pCO2air(ng)-pCO2c(i))
#   if defined TALK_ADDITION
            CO2a_Flx=cff3*CO2_sol*(pCO2air(ng)-pCO2a(i))
#   endif
# endif
            Bio(i,k,iTIC_)=Bio(i,k,iTIC_)+                              &
     &                     CO2c_Flx*Hz_inv(i,k)
# ifdef DIAGNOSTICS_BIO
            DiaBio2d(i,j,iCfxc)=DiaBio2d(i,j,iCfxc)+                    &
#  ifdef WET_DRY
     &                          rmask_full(i,j)*                        &
#  endif
     &                          CO2c_Flx*fiter
            DiaBio2d(i,j,ipCO2c)=pCO2c(i)
#  ifdef WET_DRY
            DiaBio2d(i,j,ipCO2c)=DiaBio2d(i,j,ipCO2c)*rmask_full(i,j)
#  endif
# endif
# if defined TALK_ADDITION
            Bio(i,k,idTIC)=Bio(i,k,idTIC)+                              &
     &                     (CO2a_Flx-CO2c_Flx)*Hz_inv(i,k)
#   ifdef DIAGNOSTICS_BIO
            DiaBio2d(i,j,iCfxa)=DiaBio2d(i,j,iCfxa)+                    &
#    ifdef WET_DRY
     &                          rmask_full(i,j)*                        &
#    endif
     &                          CO2a_Flx*fiter
            DiaBio2d(i,j,ipCO2a)=pCO2a(i)
#    ifdef WET_DRY
            DiaBio2d(i,j,ipCO2a)=DiaBio2d(i,j,ipCO2a)*rmask_full(i,j)
#    endif
#   endif
# endif
          END DO
#endif
!
! Sediment respiration imposed at bottom
!
          DO i=Istr,Iend
            cff5=ABS(z_w(i,j,0))
#if defined SOC_HRM23 || defined SOC_H2 || defined SOC_H23
# if defined SOC_HRM23
            IF (((ng .eq. 1) .and. (i .lt. 51) .and. (j .gt. 247))      &
     &               .or. (ng .eq. 2)) THEN
# elif defined SOC_H2
            IF (((ng .eq. 1) .and. ((j .gt. 340) .or. ((i .gt. 135)     &
     &              .and. j .gt. 320 ))) .and. (j .lt. 390)) THEN
# elif defined SOC_H3
            IF (((ng .eq. 1) .and. ((j .gt. 150) .or. ((i .gt. 50)      &
     &                 .and. j .gt. 92 )) .and. (j .lt. 302))) THEN
# elif defined SOC_H23
            IF (((ng .eq. 1) .and. ((j .gt. 340) .or. ((i .gt. 135)     &
     &               .and. j .gt. 320 )) .and. (j .lt. 390)) .or.       &
                ((ng .eq. 2) .and. ((j .gt. 150) .or. ((i .gt. 50)      &
     &               .and. j .gt. 92 )) .and. (j .lt. 302))) THEN
# else
            IF ((ng .eq. 0)) THEN
# endif
# ifdef SOC_ZVAR
!             Based on DIC vs depth relationship from Chris Algar
              cff2=3.37_r8*EXP(0.033*cff5)*1.0_r8 ! warning, multiplier
              cff1=cff2*Hz_inv(i,1)*dtdays
# else
              cff1=SOC(ng)*Hz_inv(i,1)*dtdays
# endif
!               cff1=100_r8*Hz_inv(i,1)*dtdays
# if defined SOC_H2
            ELSEIF ((j .lt. 390)) THEN
# elif defined SOC_H3
            ELSEIF ((j .lt. 302)) THEN
# elif defined SOC_H23
            ELSEIF (((ng .eq. 1) .and. (j .lt. 390)) .or.               &
                    ((ng .eq. 2) .and. (j .lt. 302))) THEN
# else
            ELSE
# endif
              cff2=SOC0*cff5+SOC1*cff5**2+SOC2*cff5**3+SOC3*            &
     &             lonr(i,j)+SOC4*latr(i,j)+SOC5*Bio(i,1,isalt)+        &
     &             SOC6*Bio(i,1,isalt)**2+SOC7*Bio(i,1,isalt)**3+       &
     &             SOC8*Bio(i,1,itemp)+SOC9*Bio(i,1,itemp)**2+          &
     &             SOC10*Bio(i,1,itemp)**3+SOC11*Bio(i,N(ng),isalt)+    &
     &             SOC12*Bio(i,N(ng),isalt)**2+SOC13*                   &
     &             Bio(i,N(ng),isalt)**3+SOC14*Bio(i,N(ng),itemp)+      &
     &             SOC15*Bio(i,N(ng),itemp)**2+SOC16*                   &
     &             Bio(i,N(ng),itemp)**3+SOC17
                   cff2=MIN(MAX(cff2,0.0_r8),30.0_r8)
              cff1=cff2*Hz_inv(i,1)*dtdays
# if defined SOC_H2 || defined SOC_H3 || defined SOC_H23
            ELSE
              cff1=0.0_r8
# endif
            END IF
#elif defined SOC_NWA
            IF ((latr(i,j) .ge. 53.45_r8) .or.                          &
     &            ((latr(i,j) .ge. 50_r8) .and.                         &
     &            (lonr(i,j) .le. -76_r8))) THEN    ! Northern area
                IF ((cff5 .lt. 200_r8)) THEN        ! Shef area
                  cff2=(SOC0(1)+SOC0(2)*latr(i,j)+SOC0(3)*lonr(i,j)+    &
     &                SOC0(4)*cff5+SOC0(5)*fac6+SOC0(6)*fac7)*115/12
                ELSEIF ((cff5 .lt. 3000_r8)) THEN   ! Slope area
                  cff2=(SOC1(1)+SOC1(2)*latr(i,j)+SOC1(3)*lonr(i,j)+    &
     &                SOC1(4)*cff5+SOC1(5)*fac6+SOC1(6)*fac7)*115/12
                ELSE                                ! Deep area
                  cff2=0.02_r8
                END IF
            ELSE                   ! Southern area
                IF ((cff5 .lt. 200_r8)) THEN        ! Shef area
                  cff2=(SOC2(1)+SOC2(2)*latr(i,j)+SOC2(3)*lonr(i,j)+    &
     &                SOC2(4)*cff5+SOC2(5)*fac6+SOC2(6)*fac7)*115/12
                ELSEIF ((cff5 .lt. 3000_r8)) THEN   ! Slope area
                   cff2=(SOC3(1)+SOC3(2)*latr(i,j)+SOC3(3)*lonr(i,j)+   &
     &                 SOC3(4)*cff5+SOC3(5)*fac6+SOC3(6)*fac7)*115/12
                ELSE                                ! Deep area
                    cff2=0.02_r8
                END IF
            END IF
            cff1=cff2*Hz_inv(i,1)*dtdays
#else
#ifdef SOC_ZVAR
            cff2=3.37_r8*EXP(0.033*cff5)*1.0_r8 ! warning, multiplier
            cff1=cff2*Hz_inv(i,1)*dtdays
# else
            cff1=SOC(ng)*Hz_inv(i,1)*dtdays
# endif
#endif
#ifdef OXYGEN
# ifdef SOC_OXYDEP
            Bio(i,1,iOxyg)=Bio(i,1,iOxyg)-cff1*(1.0_r8-                 &
     &                     EXP(-MAX(Bio(i,1,iOxyg),0.0_r8)/30.0_r8))
# else
            Bio(i,1,iOxyg)=Bio(i,1,iOxyg)-cff1
# endif
#endif
#ifdef CARBON
            Bio(i,1,iTIC_)=Bio(i,1,iTIC_)+cff1
# ifdef TALK_NONCONSERV
#  ifdef TALK_BGC
            Bio(i,1,iTAlk)=Bio(i,1,iTAlk)+cff1*0.036_r8
#  endif
# endif
#endif
          END DO
#ifdef TALK_ADDITION
!
!-----------------------------------------------------------------------
!  Vertical sinking terms: particles
!-----------------------------------------------------------------------
!
!  Reconstruct vertical profile of selected biological constituents
!  "Bio(:,:,isink)" in terms of a set of parabolic segments within each
!  grid box. Then, compute semi-Lagrangian flux due to sinking.
!
          SINK_LOOP: DO isink=1,Nsink
            ibio=idsink(isink)
!
!  Copy concentration of biological particulates into scratch array
!  "qc" (q-central, restrict it to be positive) which is hereafter
!  interpreted as a set of grid-box averaged values for biogeochemical
!  constituent concentration.
!
            DO k=1,N(ng)
              DO i=Istr,Iend
                qc(i,k)=Bio(i,k,ibio)
              END DO
            END DO
!
            DO k=N(ng)-1,1,-1
              DO i=Istr,Iend
                FC(i,k)=(qc(i,k+1)-qc(i,k))*Hz_inv2(i,k)
              END DO
            END DO
            DO k=2,N(ng)-1
              DO i=Istr,Iend
                dltR=Hz(i,j,k)*FC(i,k)
                dltL=Hz(i,j,k)*FC(i,k-1)
                cff=Hz(i,j,k-1)+2.0_r8*Hz(i,j,k)+Hz(i,j,k+1)
                cffR=cff*FC(i,k)
                cffL=cff*FC(i,k-1)
!
!  Apply PPM monotonicity constraint to prevent oscillations within the
!  grid box.
!
                IF ((dltR*dltL).le.0.0_r8) THEN
                  dltR=0.0_r8
                  dltL=0.0_r8
                ELSE IF (ABS(dltR).gt.ABS(cffL)) THEN
                  dltR=cffL
                ELSE IF (ABS(dltL).gt.ABS(cffR)) THEN
                  dltL=cffR
                END IF
!
!  Compute right and left side values (bR,bL) of parabolic segments
!  within grid box Hz(k); (WR,WL) are measures of quadratic variations.
!
!  NOTE: Although each parabolic segment is monotonic within its grid
!        box, monotonicity of the whole profile is not guaranteed,
!        because bL(k+1)-bR(k) may still have different sign than
!        qc(i,k+1)-qc(i,k).  This possibility is excluded,
!        after bL and bR are reconciled using WENO procedure.
!
                cff=(dltR-dltL)*Hz_inv3(i,k)
                dltR=dltR-cff*Hz(i,j,k+1)
                dltL=dltL+cff*Hz(i,j,k-1)
                bR(i,k)=qc(i,k)+dltR
                bL(i,k)=qc(i,k)-dltL
                WR(i,k)=(2.0_r8*dltR-dltL)**2
                WL(i,k)=(dltR-2.0_r8*dltL)**2
              END DO
            END DO
            cff=1.0E-14_r8
            DO k=2,N(ng)-2
              DO i=Istr,Iend
                dltL=MAX(cff,WL(i,k  ))
                dltR=MAX(cff,WR(i,k+1))
                bR(i,k)=(dltR*bR(i,k)+dltL*bL(i,k+1))/(dltR+dltL)
                bL(i,k+1)=bR(i,k)
              END DO
            END DO
            DO i=Istr,Iend
              FC(i,N(ng))=0.0_r8            ! NO-flux boundary condition
#if defined LINEAR_CONTINUATION
              bL(i,N(ng))=bR(i,N(ng)-1)
              bR(i,N(ng))=2.0_r8*qc(i,N(ng))-bL(i,N(ng))
#elif defined NEUMANN
              bL(i,N(ng))=bR(i,N(ng)-1)
              bR(i,N(ng))=1.5_r8*qc(i,N(ng))-0.5_r8*bL(i,N(ng))
#else
              bR(i,N(ng))=qc(i,N(ng))       ! default strictly monotonic
              bL(i,N(ng))=qc(i,N(ng))       ! conditions
              bR(i,N(ng)-1)=qc(i,N(ng))
#endif
#if defined LINEAR_CONTINUATION
              bR(i,1)=bL(i,2)
              bL(i,1)=2.0_r8*qc(i,1)-bR(i,1)
#elif defined NEUMANN
              bR(i,1)=bL(i,2)
              bL(i,1)=1.5_r8*qc(i,1)-0.5_r8*bR(i,1)
#else
              bL(i,2)=qc(i,1)               ! bottom grid boxes are
              bR(i,1)=qc(i,1)               ! re-assumed to be
              bL(i,1)=qc(i,1)               ! piecewise constant.
#endif
            END DO
!
!  Apply monotonicity constraint again, since the reconciled interfacial
!  values may cause a non-monotonic behavior of the parabolic segments
!  inside the grid box.
!
            DO k=1,N(ng)
              DO i=Istr,Iend
                dltR=bR(i,k)-qc(i,k)
                dltL=qc(i,k)-bL(i,k)
                cffR=2.0_r8*dltR
                cffL=2.0_r8*dltL
                IF ((dltR*dltL).lt.0.0_r8) THEN
                  dltR=0.0_r8
                  dltL=0.0_r8
                ELSE IF (ABS(dltR).gt.ABS(cffL)) THEN
                  dltR=cffL
                ELSE IF (ABS(dltL).gt.ABS(cffR)) THEN
                  dltL=cffR
                END IF
                bR(i,k)=qc(i,k)+dltR
                bL(i,k)=qc(i,k)-dltL
              END DO
            END DO
!
!  After this moment reconstruction is considered complete. The next
!  stage is to compute vertical advective fluxes, FC. It is expected
!  that sinking may occurs relatively fast, the algorithm is designed
!  to be free of CFL criterion, which is achieved by allowing
!  integration bounds for semi-Lagrangian advective flux to use as
!  many grid boxes in upstream direction as necessary.
!
!  In the two code segments below, WL is the z-coordinate of the
!  departure point for grid box interface z_w with the same indices;
!  FC is the finite volume flux; ksource(:,k) is index of vertical
!  grid box which contains the departure point (restricted by N(ng)).
!  During the search: also add in content of whole grid boxes
!  participating in FC.
!
            cff=dtdays*ABS(Wbio(isink))
            DO k=1,N(ng)
              DO i=Istr,Iend
                FC(i,k-1)=0.0_r8
                WL(i,k)=z_w(i,j,k-1)+cff
                WR(i,k)=Hz(i,j,k)*qc(i,k)
                ksource(i,k)=k
              END DO
            END DO
            DO k=1,N(ng)
              DO ks=k,N(ng)-1
                DO i=Istr,Iend
                  IF (WL(i,k).gt.z_w(i,j,ks)) THEN
                    ksource(i,k)=ks+1
                    FC(i,k-1)=FC(i,k-1)+WR(i,ks)
                  END IF
                END DO
              END DO
            END DO
!
!  Finalize computation of flux: add fractional part.
!
            DO k=1,N(ng)
              DO i=Istr,Iend
                ks=ksource(i,k)
                cu=MIN(1.0_r8,(WL(i,k)-z_w(i,j,ks-1))*Hz_inv(i,ks))
                FC(i,k-1)=FC(i,k-1)+                                    &
     &                    Hz(i,j,ks)*cu*                                &
     &                    (bL(i,ks)+                                    &
     &                     cu*(0.5_r8*(bR(i,ks)-bL(i,ks))-              &
     &                         (1.5_r8-cu)*                             &
     &                         (bR(i,ks)+bL(i,ks)-                      &
     &                          2.0_r8*qc(i,ks))))
              END DO
            END DO
            DO k=1,N(ng)
              DO i=Istr,Iend
                Bio(i,k,ibio)=qc(i,k)+(FC(i,k)-FC(i,k-1))*Hz_inv(i,k)
              END DO
            END DO

!
!  Particles reaching the seafloor stay at the bottom. Without this
!  module, particles falls out of the system. 
!
            IF (ibio.eq.iTAp1) THEN
              DO i=Istr,Iend
                cff1=FC(i,0)*Hz_inv(i,1)
                Bio(i,1,iTAp1)=Bio(i,1,iTAp1)+cff1
              END DO
# if defined TALK_TWO_FEED || defined TALK_THREE_FEED
            ELSE IF (ibio.eq.iTAp2) THEN
              DO i=Istr,Iend
                cff1=FC(i,0)*Hz_inv(i,1)
                Bio(i,1,iTAp2)=Bio(i,1,iTAp2)+cff1
              END DO
# endif
# if defined TALK_THREE_FEED
            ELSE IF (ibio.eq.iTAp3) THEN
              DO i=Istr,Iend
                cff1=FC(i,0)*Hz_inv(i,1)
                Bio(i,1,iTAp3)=Bio(i,1,iTAp3)+cff1
              END DO
# endif
            END IF
          END DO SINK_LOOP
#endif
        END DO ITER_LOOP
!
!-----------------------------------------------------------------------
!  Update global tracer variables: Add increment due to BGC processes
!  to tracer array in time index "nnew". Index "nnew" is solution after
!  advection and mixing and has transport units (m Tunits) hence the
!  increment is multiplied by Hz.  Notice that we need to subtract
!  original values "Bio_old" at the top of the routine to just account
!  for the concentractions affected by BGC processes. This also takes
!  into account any constraints (non-negative concentrations, carbon
!  concentration range) specified before entering BGC kernel. If "Bio"
!  were unchanged by BGC processes, the increment would be exactly
!  zero. Notice that final tracer values, t(:,:,:,nnew,:) are not
!  bounded >=0 so that we can preserve total inventory of N and
!  C even when advection causes tracer concentration to go negative.
!  (J. Wilkin and H. Arango, Apr 27, 2012)
!-----------------------------------------------------------------------
!
#ifdef CARBON
        DO k=1,N(ng)
          DO i=Istr,Iend
            Bio(i,k,iTIC_)=MIN(Bio(i,k,iTIC_),3000.0_r8)
            Bio(i,k,iTIC_)=MAX(Bio(i,k,iTIC_),400.0_r8)
          END DO
        END DO
#endif
        DO itrc=1,NBT
          ibio=idbio(itrc)
          DO k=1,N(ng)
            DO i=Istr,Iend
              cff=Bio(i,k,ibio)-Bio_old(i,k,ibio)
#ifdef MASKING
              cff=cff*rmask(i,j)
# ifdef WET_DRY
              cff=cff*rmask_wet(i,j)
# endif
#endif
              t(i,j,k,nnew,ibio)=t(i,j,k,nnew,ibio)+cff*Hz(i,j,k)
            END DO
          END DO
        END DO
      END DO J_LOOP
!
      RETURN
      END SUBROUTINE biology_tile

#ifdef CARBON
# ifdef pCO2_RZ
      SUBROUTINE pCO2_water_RZ (Istr, Iend,                             &
     &                          LBi, UBi, LBj, UBj, IminS, ImaxS,       &
     &                          j, DoNewton,                            &
#  ifdef MASKING
     &                          rmask,                                  &
#  endif
     &                          T, S, TIC, TAlk, pH, pCO2)
!
!***********************************************************************
!                                                                      !
!  This routine computes equilibrium partial pressure of CO2 (pCO2)    !
!  in the surface seawater.                                            !
!                                                                      !
!  On Input:                                                           !
!                                                                      !
!     Istr       Starting tile index in the I-direction.               !
!     Iend       Ending   tile index in the I-direction.               !
!     LBi        I-dimension lower bound.                              !
!     UBi        I-dimension upper bound.                              !
!     LBj        J-dimension lower bound.                              !
!     UBj        J-dimension upper bound.                              !
!     IminS      I-dimension lower bound for private arrays.           !
!     ImaxS      I-dimension upper bound for private arrays.           !
!     j          j-pipelined index.                                    !
!     DoNewton   Iteration solver:                                     !
!                  [0] Bracket and bisection.                          !
!                  [1] Newton-Raphson method.                          !
!     rmask      Land/Sea masking.                                     !
!     T          Surface temperature (Celsius).                        !
!     S          Surface salinity (PSS).                               !
!     TIC        Total inorganic carbon (millimol/m3).                 !
!     TAlk       Total alkalinity (milli-equivalents/m3).              !
!     pH         Best pH guess.                                        !
!                                                                      !
!  On Output:                                                          !
!                                                                      !
!     pCO2       partial pressure of CO2 (ppmv).                       !
!                                                                      !
!  Check Value:  (T=24, S=36.6, TIC=2040, TAlk=2390, PO4b=0,           !
!                 SiO3=0, pH=8)                                        !
!                                                                      !
!                pcO2= ppmv  (DoNewton=0)                              !
!                pCO2= ppmv  (DoNewton=1)                              !
!                                                                      !
!  This subroutine was adapted by Katja Fennel (Nov 2005) from         !
!  Zeebe and Wolf-Gladrow (2001).                                      !
!                                                                      !
!  Reference:                                                          !
!                                                                      !
!    Zeebe, R.E. and D. Wolf-Gladrow,  2005:  CO2 in Seawater:         !
!      Equilibrium, kinetics, isotopes, Elsevier Oceanographic         !
!      Series, 65, pp 346.                                             !
!                                                                      !
!***********************************************************************
!
      USE mod_kinds
!
      implicit none
!
!  Imported variable declarations.
!
      integer,  intent(in) :: LBi, UBi, LBj, UBj, IminS, ImaxS
      integer,  intent(in) :: Istr, Iend, j, DoNewton
!
!#  ifdef ASSUMED_SHAPE
!#   ifdef MASKING
!      real(r8), intent(in) :: rmask(LBi:,LBj:)
!#   endif
!      real(r8), intent(in) :: T(IminS:)
!      real(r8), intent(in) :: S(IminS:)
!      real(r8), intent(in) :: TIC(IminS:)
!      real(r8), intent(in) :: TAlk(IminS:)
!      real(r8), intent(inout) :: pH(LBi:,LBj:)
!#  else
#   ifdef MASKING
      real(r8), intent(in) :: rmask(LBi:UBi,LBj:UBj)
#   endif
      real(r8), intent(in) :: T(IminS:ImaxS)
      real(r8), intent(in) :: S(IminS:ImaxS)
      real(r8), intent(in) :: TIC(IminS:ImaxS)
      real(r8), intent(in) :: TAlk(IminS:ImaxS)
      real(r8), intent(inout) :: pH(LBi:UBi,LBj:UBj)
!#  endif

      real(r8), intent(out) :: pCO2(IminS:ImaxS)
!
!  Local variable declarations.
!
      integer, parameter :: InewtonMax = 10
      integer, parameter :: IbrackMax = 30

      integer :: Hstep, Ibrack, Inewton, i

      real(r8) :: Tk, centiTk, invTk, logTk
      real(r8) :: scl, sqrtS
      real(r8) :: borate, alk, dic
      real(r8) :: ff, K1, K2, K12, Kb, Kw
      real(r8) :: p5, p4, p3, p2, p1, p0
      real(r8) :: df, fn, fni(3), ftest
      real(r8) :: deltaX, invX, invX2, X, X2, X3
      real(r8) :: pH_guess, pH_hi, pH_lo
      real(r8) :: X_guess, X_hi, X_lo, X_mid
      real(r8) :: CO2star, Htotal, Htotal2
!
!=======================================================================
!  Determine coefficients for surface carbon chemisty.  If land/sea
!  masking, compute only on water points.
!=======================================================================
!
      I_LOOP: DO i=Istr,Iend
#  ifdef MASKING
        IF (rmask(i,j).gt.0.0_r8) THEN
#  endif
        Tk=T(i)+273.15_r8
        centiTk=0.01_r8*Tk
        invTk=1.0_r8/Tk
        logTk=LOG(Tk)
        sqrtS=SQRT(S(i))
        scl=S(i)/1.80655_r8

        alk= TAlk(i)*0.000001_r8
        dic = TIC(i)*0.000001_r8
!
!-----------------------------------------------------------------------
!  Correction term for non-ideality, ff=k0*(1-pH2O). Equation 13 with
!  table 6 values from Weiss and Price (1980, Mar. Chem., 8, 347-359).
!-----------------------------------------------------------------------
!
        ff=EXP(-162.8301_r8+                                            &
     &         218.2968_r8/centiTk+                                     &
     &         LOG(centiTk)*90.9241_r8-                                 &
     &         centiTk*centiTk*1.47696_r8+                              &
     &         S(i)*(0.025695_r8-                                       &
     &               centiTk*(0.025225_r8-                              &
     &                        centiTk*0.0049867_r8)))
!
!-----------------------------------------------------------------------
!  Compute first (K1) and second (K2) dissociation constant of carboinic
!  acid:
!
!           K1 = [H][HCO3]/[H2CO3]
!           K2 = [H][CO3]/[HCO3]
!
!  From Millero (1995; page 664) using Mehrbach et al. (1973) data on
!  seawater scale.
!-----------------------------------------------------------------------
!
        K1=10.0_r8**(62.008_r8-                                         &
     &               invTk*3670.7_r8-                                   &
     &               logTk*9.7944_r8+                                   &
     &               S(i)*(0.0118_r8-                                   &
     &                     S(i)*0.000116_r8))
        K2=10.0_r8**(-4.777_r8-                                         &
     &               invTk*1394.7_r8+                                   &
     &               S(i)*(0.0184_r8-                                   &
     &                     S(i)*0.000118_r8))
!
!-----------------------------------------------------------------------
!  Compute dissociation constant of boric acid, Kb=[H][BO2]/[HBO2].
!  From Millero (1995; page 669) using data from Dickson (1990).
!-----------------------------------------------------------------------
!
        Kb=EXP(-invTk*(8966.90_r8+                                      &
     &                 sqrtS*(2890.53_r8+                               &
     &                        sqrtS*(77.942_r8-                         &
     &                               sqrtS*(1.728_r8-                   &
     &                                      sqrtS*0.0996_r8))))-        &
     &         logTk*(24.4344_r8+                                       &
     &                sqrtS*(25.085_r8+                                 &
     &                       sqrtS*0.2474_r8))+                         &
     &         Tk*(sqrtS*0.053105_r8)+                                  &
     &         148.0248_r8+                                             &
     &         sqrtS*(137.1942_r8+                                      &
     &                sqrtS*1.62142_r8))
!
!-----------------------------------------------------------------------
!  Compute ion product of whater, Kw = [H][OH].
!  From Millero (1995; page 670) using composite data.
!-----------------------------------------------------------------------
!
        Kw=EXP(148.9652_r8-                                             &
     &         invTk*13847.26_r8-                                       &
     &         logTk*23.6521_r8-                                        &
     &         sqrtS*(5.977_r8-                                         &
     &                invTk*118.67_r8-                                  &
     &                logTk*1.0495_r8)-                                 &
     &         S(i)*0.01615_r8)
!
!-----------------------------------------------------------------------
! Calculate concentrations for borate (Uppstrom, 1974).
!-----------------------------------------------------------------------
!
        borate=0.000232_r8*scl/10.811_r8
!
!=======================================================================
!  Iteratively solver for computing hydrogen ions [H+] using either:
!
!    (1) Newton-Raphson method with fixed number of iterations,
!        use previous [H+] as first guess, or
!    (2) bracket and bisection
!=======================================================================
!
!  Solve for h in fifth-order polynomial. First calculate
!  polynomial coefficients.
!
        K12 = K1*K2

        p5 = -1.0_r8;
        p4 = -alk-Kb-K1;
        p3 = dic*K1-alk*(Kb+K1)+Kb*borate+Kw-Kb*K1-K12
        p2 = dic*(Kb*K1+2*K12)-alk*(Kb*K1+K12)+Kb*borate*K1             &
     &       +(Kw*Kb+Kw*K1-Kb*K12)
        p1 = 2.0_r8*dic*Kb*K12-alk*Kb*K12+Kb*borate*K12                 &
     &       +Kw*Kb*K1+Kw*K12
        p0 = Kw*Kb*K12;
!
!  Set first guess and brackets for [H+] solvers.
!
        pH_guess=pH(i,j)         ! Newton-Raphson
        pH_hi=10.0_r8            ! high bracket/bisection
        pH_lo=5.0_r8             ! low bracket/bisection
!
!  Convert to [H+].
!
        X_guess=10.0_r8**(-pH_guess)
        X_lo=10.0_r8**(-pH_hi)
        X_hi=10.0_r8**(-pH_lo)
        X_mid=0.5_r8*(X_lo+X_hi)
!
!-----------------------------------------------------------------------
!  Newton-Raphson method.
!-----------------------------------------------------------------------
!
        IF (DoNewton.eq.1) THEN
          X=X_guess
!
          DO Inewton=1,InewtonMax
!
!  Evaluate f([H+]) = p5*x^5+...+p1*x+p0
!
            fn=((((p5*X+p4)*X+p3)*X+p2)*X+p1)*X+p0
!
!  Evaluate derivative, df([H+])/dx:
!
!     df= d(fn)/d(X)
!
            df=(((5*p5*X+4*p4)*X+3*p3)*X+2*p2)*X+p1
!
!  Evaluate increment in [H+].
!
            deltaX=-fn/df
!
!  Update estimate of [H+].
!
            X=X+deltaX
          END DO
!
!-----------------------------------------------------------------------
!  Bracket and bisection method.
!-----------------------------------------------------------------------
!
        ELSE
!
!  If first step, use Bracket and Bisection method with fixed, large
!  number of iterations
!
          BRACK_IT: DO Ibrack=1,IbrackMax
            DO Hstep=1,3
              IF (Hstep.eq.1) X=X_hi
              IF (Hstep.eq.2) X=X_lo
              IF (Hstep.eq.3) X=X_mid
!
!  Evaluate f([H+]) for bracketing and mid-value cases.
!
              fni(Hstep)=((((p5*X+p4)*X+p3)*X+p2)*X+p1)*X+p0
            END DO
!
!  Now, bracket solution within two of three.
!
            IF (fni(3).eq.0) THEN
               EXIT BRACK_IT
            ELSE
               ftest=fni(1)/fni(3)
               IF (ftest.gt.0) THEN
                 X_hi=X_mid
               ELSE
                 X_lo=X_mid
               END IF
               X_mid=0.5_r8*(X_lo+X_hi)
            END IF
          END DO BRACK_IT
!
! Last iteration gives value.
!
          X=X_mid
        END IF
!
!-----------------------------------------------------------------------
!  Determine pCO2.
!-----------------------------------------------------------------------
!
!  Total Hydrogen ion concentration, Htotal = [H+].
!
        Htotal=X
        Htotal2=Htotal*Htotal
!
!  Calculate [CO2*] (mole/m3) as defined in DOE Methods Handbook 1994
!  Version 2, ORNL/CDIAC-74, Dickson and Goyet, Eds. (Chapter 2,
!  page 10, Eq A.49).
!
        CO2star=dic*Htotal2/(Htotal2+K1*Htotal+K1*K2)
!
!  Save pH is used again outside this routine.
!
        pH(i,j)=-LOG10(Htotal)
!
!  Add two output arguments for storing pCO2surf.
!
        pCO2(i)=CO2star*1000000.0_r8/ff

#  ifdef MASKING
      ELSE
        pH(i,j)=0.0_r8
        pCO2(i)=0.0_r8
      END IF
#  endif

      END DO I_LOOP
!
      RETURN
      END SUBROUTINE pCO2_water_RZ
# else
      SUBROUTINE pCO2_water (Istr, Iend,                                &
     &                       LBi, UBi, LBj, UBj,                        &
     &                       IminS, ImaxS, j, DoNewton,                 &
#  ifdef MASKING
     &                       rmask,                                     &
#  endif
     &                       T, S, TIC, TAlk, PO4b, SiO3, pH, pCO2)
!
!***********************************************************************
!                                                                      !
!  This routine computes equilibrium partial pressure of CO2 (pCO2)    !
!  in the surface seawater.                                            !
!                                                                      !
!  On Input:                                                           !
!                                                                      !
!     Istr       Starting tile index in the I-direction.               !
!     Iend       Ending   tile index in the I-direction.               !
!     LBi        I-dimension lower bound.                              !
!     UBi        I-dimension upper bound.                              !
!     LBj        J-dimension lower bound.                              !
!     UBj        J-dimension upper bound.                              !
!     IminS      I-dimension lower bound for private arrays.           !
!     ImaxS      I-dimension upper bound for private arrays.           !
!     j          j-pipelined index.                                    !
!     DoNewton   Iteration solver:                                     !
!                  [0] Bracket and bisection.                          !
!                  [1] Newton-Raphson method.                          !
!     rmask      Land/Sea masking.                                     !
!     T          Surface temperature (Celsius).                        !
!     S          Surface salinity (PSS).                               !
!     TIC        Total inorganic carbon (millimol/m3).                 !
!     TAlk       Total alkalinity (milli-equivalents/m3).              !
!     PO4b        Inorganic phosphate (millimol/m3).                   !
!     SiO3       Inorganic silicate (millimol/m3).                     !
!     pH         Best pH guess.                                        !
!                                                                      !
!  On Output:                                                          !
!                                                                      !
!     pCO2       partial pressure of CO2 (ppmv).                       !
!                                                                      !
!  Check Value:  (T=24, S=36.6, TIC=2040, TAlk=2390, PO4b=0,           !
!                 SiO3=0, pH=8)                                        !
!                                                                      !
!                pcO2=0.35074945E+03 ppmv  (DoNewton=0)                !
!                pCO2=0.35073560E+03 ppmv  (DoNewton=1)                !
!                                                                      !
!  This subroutine was adapted by Mick Follows (Oct 1999) from OCMIP2  !
!  code CO2CALC. Modified for ROMS by Hernan Arango (Nov 2003).        !
!                                                                      !
!***********************************************************************
!
      USE mod_kinds
!
      implicit none
!
!  Imported variable declarations.
!
      integer,  intent(in) :: LBi, UBi, LBj, UBj, IminS, ImaxS
      integer,  intent(in) :: Istr, Iend, j, DoNewton
!
!#  ifdef ASSUMED_SHAPE
!#   ifdef MASKING
!      real(r8), intent(in) :: rmask(LBi:,LBj:)
!#   endif
!      real(r8), intent(in) :: T(IminS:)
!      real(r8), intent(in) :: S(IminS:)
!      real(r8), intent(in) :: TIC(IminS:)
!      real(r8), intent(in) :: TAlk(IminS:)
!      real(r8), intent(inout) :: pH(LBi:,LBj:)
!#  else
#   ifdef MASKING
      real(r8), intent(in) :: rmask(LBi:UBi,LBj:UBj)
#   endif
      real(r8), intent(in) :: T(IminS:ImaxS)
      real(r8), intent(in) :: S(IminS:ImaxS)
      real(r8), intent(in) :: TIC(IminS:ImaxS)
      real(r8), intent(in) :: TAlk(IminS:ImaxS)
      real(r8), intent(inout) :: pH(LBi:UBi,LBj:UBj)
!#  endif
      real(r8), intent(in) :: PO4b
      real(r8), intent(in) :: SiO3

      real(r8), intent(out) :: pCO2(IminS:ImaxS)
!
!  Local variable declarations.
!
      integer, parameter :: InewtonMax = 10
      integer, parameter :: IbrackMax = 30

      integer :: Hstep, Ibrack, Inewton, i

      real(r8) :: Tk, centiTk, invTk, logTk
      real(r8) :: SO4, scl, sqrtS, sqrtSO4
      real(r8) :: alk, dic, phos, sili
      real(r8) :: borate, sulfate, fluoride
      real(r8) :: ff, K1, K2, K1p, K2p, K3p, Kb, Kf, Ks, Ksi, Kw
      real(r8) :: K12, K12p, K123p, invKb, invKs, invKsi
      real(r8) :: A, A2, B, B2, C, dA, dB
      real(r8) :: df, fn, fni(3), ftest
      real(r8) :: deltaX, invX, invX2, X, X2, X3
      real(r8) :: pH_guess, pH_hi, pH_lo
      real(r8) :: X_guess, X_hi, X_lo, X_mid
      real(r8) :: CO2star, Htotal, Htotal2
!
!=======================================================================
!  Determine coefficients for surface carbon chemisty.  If land/sea
!  masking, compute only on water points.
!=======================================================================
!
      I_LOOP: DO i=Istr,Iend
#  ifdef MASKING
        IF (rmask(i,j).gt.0.0_r8) THEN
#  endif
        Tk=T(i)+273.15_r8
        centiTk=0.01_r8*Tk
        invTk=1.0_r8/Tk
        logTk=LOG(Tk)
        sqrtS=SQRT(S(i))
        SO4=19.924_r8*S(i)/(1000.0_r8-1.005_r8*S(i))
        sqrtSO4=SQRT(SO4)
        scl=S(i)/1.80655_r8

        alk=TAlk(i)*0.000001_r8
        dic=TIC(i)*0.000001_r8
        phos=PO4b*0.000001_r8
        sili=SiO3*0.000001_r8
!
!-----------------------------------------------------------------------
!  Correction term for non-ideality, ff=k0*(1-pH2O). Equation 13 with
!  table 6 values from Weiss and Price (1980, Mar. Chem., 8, 347-359).
!-----------------------------------------------------------------------
!
        ff=EXP(-162.8301_r8+                                            &
     &         218.2968_r8/centiTk+                                     &
     &         LOG(centiTk)*90.9241_r8-                                 &
     &         centiTk*centiTk*1.47696_r8+                              &
     &         S(i)*(0.025695_r8-                                       &
     &               centiTk*(0.025225_r8-                              &
     &                        centiTk*0.0049867_r8)))
!
!-----------------------------------------------------------------------
!  Compute first (K1) and second (K2) dissociation constant of carboinic
!  acid:
!
!           K1 = [H][HCO3]/[H2CO3]
!           K2 = [H][CO3]/[HCO3]
!
!  From Millero (1995; page 664) using Mehrbach et al. (1973) data on
!  seawater scale.
!-----------------------------------------------------------------------
!
        K1=10.0_r8**(62.008_r8-                                         &
     &               invTk*3670.7_r8-                                   &
     &               logTk*9.7944_r8+                                   &
     &               S(i)*(0.0118_r8-                                   &
     &                     S(i)*0.000116_r8))
        K2=10.0_r8**(-4.777_r8-                                         &
     &               invTk*1394.7_r8+                                   &
     &               S(i)*(0.0184_r8-                                   &
     &                     S(i)*0.000118_r8))
!
!-----------------------------------------------------------------------
!  Compute dissociation constant of boric acid, Kb=[H][BO2]/[HBO2].
!  From Millero (1995; page 669) using data from Dickson (1990).
!-----------------------------------------------------------------------
!
        Kb=EXP(-invTk*(8966.90_r8+                                      &
     &                 sqrtS*(2890.53_r8+                               &
     &                        sqrtS*(77.942_r8-                         &
     &                               sqrtS*(1.728_r8-                   &
     &                                      sqrtS*0.0996_r8))))-        &
     &         logTk*(24.4344_r8+                                       &
     &                sqrtS*(25.085_r8+                                 &
     &                       sqrtS*0.2474_r8))+                         &
     &         Tk*(sqrtS*0.053105_r8)+                                  &
     &         148.0248_r8+                                             &
     &         sqrtS*(137.1942_r8+                                      &
     &                sqrtS*1.62142_r8))
!
!-----------------------------------------------------------------------
!  Compute first (K1p), second (K2p), and third (K3p) dissociation
!  constant of phosphoric acid:
!
!           K1p = [H][H2PO4]/[H3PO4]
!           K2p = [H][HPO4]/[H2PO4]
!           K3p = [H][PO4]/[HPO4]
!
!  From DOE (1994) equations 7.2.20, 7.2.23, and 7.2.26, respectively.
!  With footnote using data from Millero (1974).
!-----------------------------------------------------------------------
!
        K1p=EXP(115.525_r8-                                             &
     &          invTk*4576.752_r8-                                      &
     &          logTk*18.453_r8+                                        &
     &          sqrtS*(0.69171_r8-invTk*106.736_r8)-                    &
     &          S(i)*(0.01844_r8+invTk*0.65643_r8))
        K2p=EXP(172.0883_r8-                                            &
     &          invTk*8814.715_r8-                                      &
     &          logTk*27.927_r8+                                        &
     &          sqrtS*(1.3566_r8-invTk*160.340_r8)-                     &
     &          S(i)*(0.05778_r8-invTk*0.37335_r8))
        K3p=EXP(-18.141_r8-                                             &
     &          invTk*3070.75_r8+                                       &
     &          sqrtS*(2.81197_r8+invTk*17.27039_r8)-                   &
     &          S(i)*(0.09984_r8+invTk*44.99486_r8))
!
!-----------------------------------------------------------------------
!  Compute dissociation constant of silica, Ksi=[H][SiO(OH)3]/[Si(OH)4].
!  From Millero (1995; page 671) using data from Yao and Millero (1995).
!-----------------------------------------------------------------------
!
        Ksi=EXP(117.385_r8-                                             &
     &          invTk*8904.2_r8-                                        &
     &          logTk*19.334_r8+                                        &
     &          sqrtSO4*(3.5913_r8-invTk*458.79_r8)-                    &
     &          SO4*(1.5998_r8-invTk*188.74_r8-                         &
     &               SO4*(0.07871_r8-invTk*12.1652_r8))+                &
     &          LOG(1.0_r8-0.001005_r8*S(i)))
!
!-----------------------------------------------------------------------
!  Compute ion product of whater, Kw = [H][OH].
!  From Millero (1995; page 670) using composite data.
!-----------------------------------------------------------------------
!
        Kw=EXP(148.9652_r8-                                             &
     &         invTk*13847.26_r8-                                       &
     &         logTk*23.6521_r8-                                        &
     &         sqrtS*(5.977_r8-                                         &
     &                invTk*118.67_r8-                                  &
     &                logTk*1.0495_r8)-                                 &
     &         S(i)*0.01615_r8)
!
!------------------------------------------------------------------------
!  Compute salinity constant of hydrogen sulfate, Ks = [H][SO4]/[HSO4].
!  From Dickson (1990, J. chem. Thermodynamics 22, 113)
!------------------------------------------------------------------------
!
        Ks=EXP(141.328_r8-                                              &
     &         invTk*4276.1_r8-                                         &
     &         logTk*23.093_r8+                                         &
     &         sqrtSO4*(324.57_r8-invTk*13856.0_r8-logTk*47.986_r8-     &
     &                  SO4*invTk*2698.0_r8)-                           &
     &         SO4*(771.54_r8-invTk*35474.0_r8-logTk*114.723_r8-        &
     &              SO4*invTk*1776.0_r8)+                               &
     &         LOG(1.0_r8-0.001005_r8*S(i)))
!
!-----------------------------------------------------------------------
!  Compute stability constant of hydrogen fluorid, Kf = [H][F]/[HF].
!  From Dickson and Riley (1979) -- change pH scale to total.
!-----------------------------------------------------------------------
!
        Kf=EXP(-12.641_r8+                                              &
     &         invTk*1590.2_r8+                                         &
     &         sqrtSO4*1.525_r8+                                        &
     &         LOG(1.0_r8-0.001005_r8*S(i))+                            &
     &         LOG(1.0_r8+0.1400_r8*scl/(96.062_r8*Ks)))
!
!-----------------------------------------------------------------------
! Calculate concentrations for borate (Uppstrom, 1974), sulfate (Morris
! and Riley, 1966), and fluoride (Riley, 1965).
!-----------------------------------------------------------------------
!
        borate=0.000232_r8*scl/10.811_r8
        sulfate=0.14_r8*scl/96.062_r8
        fluoride=0.000067_r8*scl/18.9984_r8
!
!=======================================================================
!  Iteratively solver for computing hydrogen ions [H+] using either:
!
!    (1) Newton-Raphson method with fixed number of iterations,
!        use previous [H+] as first guess, or
!    (2) bracket and bisection
!=======================================================================
!
!  Set first guess and brackets for [H+] solvers.
!
        pH_guess=pH(i,j)         ! Newton-Raphson
        pH_hi=10.0_r8            ! high bracket/bisection
        pH_lo=5.0_r8             ! low bracket/bisection
!
!  Convert to [H+].
!
        X_guess=10.0_r8**(-pH_guess)
        X_lo=10.0_r8**(-pH_hi)
        X_hi=10.0_r8**(-pH_lo)
        X_mid=0.5_r8*(X_lo+X_hi)
!
!-----------------------------------------------------------------------
!  Newton-Raphson method.
!-----------------------------------------------------------------------
!
        IF (DoNewton.eq.1) THEN
          X=X_guess
          K12=K1*K2
          K12p=K1p*K2p
          K123p=K12p*K3p
          invKb=1.0_r8/Kb
          invKs=1.0_r8/Ks
          invKsi=1.0_r8/Ksi
!
          DO Inewton=1,InewtonMax
!
!  Set some common combinations of parameters used in the iterative [H+]
!  solver.
!
            X2=X*X
            X3=X2*X
            invX=1.0_r8/X
            invX2=1.0_r8/X2

            A=X*(K12p+X*(K1p+X))
            B=X*(K1+X)+K12
            C=1.0_r8/(1.0_r8+sulfate*invKs)

            A2=A*A
            B2=B*B
            dA=X*(2.0_r8*K1p+3.0_r8*X)+K12p
            dB=2.0_r8*X+K1
!
!  Evaluate f([H+]):
!
!     fn=HCO3+CO3+borate+OH+HPO4+2*PO4+H3PO4+silicate+Hfree+HSO4+HF-TALK
!
            fn=dic*K1*(X+2.0_r8*K2)/B+                                  &
     &         borate/(1.0_r8+X*invKb)+                                 &
     &         Kw*invX+                                                 &
     &         phos*(K12p*X+2.0_r8*K123p-X3)/A+                         &
     &         sili/(1.0_r8+X*invKsi)-                                  &
     &         X*C-                                                     &
     &         sulfate/(1.0_r8+Ks*invX*C)-                              &
     &         fluoride/(1.0_r8+Kf*invX)-                               &
     &         alk
!
!  Evaluate derivative, f(prime)([H+]):
!
!     df= d(fn)/d(X)
!
            df=dic*K1*(B-dB*(X+2.0_r8*K2))/B2-                          &
     &         borate/(invKb*(1.0+X*invKb)**2)-                         &
     &         Kw*invX2+                                                &
     &         phos*(A*(K12p-3.0_r8*X2)-dA*(K12p*X+2.0_r8*K123p-X3))/A2-&
     &         sili/(invKsi*(1.0_r8+X*invKsi)**2)+                      &
     &         C+                                                       &
     &         sulfate*Ks*C*invX2/((1.0_r8+Ks*invX*C)**2)+              &
     &         fluoride*Kf*invX2/((1.0_r8+Kf*invX)**2)
!
!  Evaluate increment in [H+].
!
            deltaX=-fn/df
!
!  Update estimate of [H+].
!
            X=X+deltaX
          END DO
!
!-----------------------------------------------------------------------
!  Bracket and bisection method.
!-----------------------------------------------------------------------
!
        ELSE
!
!  If first step, use Bracket and Bisection method with fixed, large
!  number of iterations
!
          K12=K1*K2
          K12p=K1p*K2p
          K123p=K12p*K3p
          invKb=1.0_r8/Kb
          invKs=1.0_r8/Ks
          invKsi=1.0_r8/Ksi
!
          BRACK_IT: DO Ibrack=1,IbrackMax
            DO Hstep=1,3
              IF (Hstep.eq.1) X=X_hi
              IF (Hstep.eq.2) X=X_lo
              IF (Hstep.eq.3) X=X_mid
!
!  Set some common combinations of parameters used in the iterative [H+]
!  solver.
!
              X2=X*X
              X3=X2*X
              invX=1.0_r8/X

              A=X*(K12p+X*(K1p+X))+K123p
              B=X*(K1+X)+K12
              C=1.0_r8/(1.0_r8+sulfate*invKs)

              A2=A*A
              B2=B*B
              dA=X*(K1p*2.0_r8+3.0_r8*X2)+K12p
              dB=2.0_r8*X+K1
!
!  Evaluate f([H+]) for bracketing and mid-value cases.
!
              fni(Hstep)=dic*(K1*X+2.0_r8*K12)/B+                       &
     &                   borate/(1.0_r8+X*invKb)+                       &
     &                   Kw*invX+                                       &
     &                   phos*(K12p*X+2.0_r8*K123p-X3)/A+               &
     &                   sili/(1.0_r8+X*invKsi)-                        &
     &                   X*C-                                           &
     &                   sulfate/(1.0_r8+Ks*invX*C)-                    &
     &                   fluoride/(1.0_r8+Kf*invX)-                     &
     &                   alk
            END DO
!
!  Now, bracket solution within two of three.
!
            IF (fni(3).eq.0.0_r8) THEN
              EXIT BRACK_IT
            ELSE
              ftest=fni(1)/fni(3)
              IF (ftest.gt.0.0) THEN
                X_hi=X_mid
              ELSE
                X_lo=X_mid
              END IF
              X_mid=0.5_r8*(X_lo+X_hi)
            END IF
          END DO BRACK_IT
!
! Last iteration gives value.
!
          X=X_mid
        END IF
!
!-----------------------------------------------------------------------
!  Determine pCO2.
!-----------------------------------------------------------------------
!
!  Total Hydrogen ion concentration, Htotal = [H+].
!
        Htotal=X
        Htotal2=Htotal*Htotal
!
!  Calculate [CO2*] (mole/m3) as defined in DOE Methods Handbook 1994
!  Version 2, ORNL/CDIAC-74, Dickson and Goyet, Eds. (Chapter 2,
!  page 10, Eq A.49).
!
        CO2star=dic*Htotal2/(Htotal2+K1*Htotal+K1*K2)
!
!  Save pH is used again outside this routine.
!
        pH(i,j)=-LOG10(Htotal)
!
!  Add two output arguments for storing pCO2surf.
!
        pCO2(i)=CO2star*1000000.0_r8/ff

#  ifdef MASKING
      ELSE
        pH(i,j)=0.0_r8
        pCO2(i)=0.0_r8
      END IF
#  endif

      END DO I_LOOP
!
      RETURN
      END SUBROUTINE pCO2_water
# endif
#endif
      END MODULE biology_mod
